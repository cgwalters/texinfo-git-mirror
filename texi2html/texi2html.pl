#@BANGPERL@
'di ';
'ig 00 ';
#+##############################################################################
#
# texi2html: Program to transform Texinfo documents to HTML
#
#    Copyright (C) 1999, 2000  Free Software Foundation, Inc.
#
#    This program is free software; you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation; either version 2 of the License, or
#    (at your option) any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with this program; if not, write to the Free Software
#    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
#
#-##############################################################################

# This requires perl version 5 or higher
require 5.0;

# Perl pragma to restrict unsafe constructs
use strict;
# for POSIX::setlocale
require 5.004;
# used in case of tests, to revert to "C" locale.
use POSIX qw(setlocale LC_ALL LC_CTYPE);
#
# According to
# larry.jones@sdrc.com (Larry Jones)
# this pragma is not present in perl5.004_02:
#
# Perl pragma to control optional warnings
# use warnings;

# Declarations. Empty lines separate the different classes of variables:

# latex2html variables
# customization options variables
# customization variables
# customization variables which may be guessed in the script
# customizable subroutines references
# global variables set in the script, and used in the subroutines
# hash which entries might be redefined by the user
# variables which might be redefined by the user but aren't likely to
# be  
# variable which shouldn't be global FIXME
use vars qw(
            $ADDRESS
            $ANTI_ALIAS
            $ANTI_ALIAS_TEXT
            $ASCII_MODE
            $AUTO_LINK
            $AUTO_PREFIX
            $CHILDLINE
            $DEBUG
            $DESTDIR
            $ERROR
            $EXTERNAL_FILE
            $EXTERNAL_IMAGES
            $EXTERNAL_UP_LINK
            $EXTERNAL_UP_TITLE
            $FIGURE_SCALE_FACTOR
            $HTML_VERSION
            $IMAGES_ONLY
            $INFO
            $LINE_WIDTH
            $LOCAL_ICONS
            $LONG_TITLES
            $MATH_SCALE_FACTOR
            $MAX_LINK_DEPTH
            $MAX_SPLIT_DEPTH
            $NETSCAPE_HTML
            $NOLATEX
            $NO_FOOTNODE
            $NO_IMAGES
            $NO_NAVIGATION
            $NO_SIMPLE_MATH
            $NO_SUBDIR
            $PAPERSIZE
            $PREFIX
            $PS_IMAGES
            $REUSE
            $SCALABLE_FONTS
            $SHORTEXTN
            $SHORT_INDEX
            $SHOW_SECTION_NUMBERS
            $SPLIT
            $TEXDEFS
            $TITLE
            $TITLES_LANGUAGE
            $TMP
            $VERBOSE
            $WORDS_IN_NAVIGATION_PANEL_TITLES
            $WORDS_IN_PAGE

            $T2H_DEBUG
            $T2H_PREFIX
            $T2H_VERBOSE
            $T2H_SUBDIR
            $T2H_IDX_SUMMARY
            $T2H_SPLIT
            $T2H_SHORT_REF
            @T2H_EXPAND
            $T2H_TOC
            $T2H_TOP
            $T2H_DOCTYPE 
            $T2H_FRAMESET_DOCTYPE 
            $T2H_CHECK 
            $T2H_TEST 
            $T2H_USE_GLOSSARY 
            $T2H_INVISIBLE_MARK 
            $T2H_USE_ISO 
            $T2H_TOP_FILE 
            $T2H_TOC_FILE
            $T2H_FRAMES
            $T2H_SHOW_MENU
            $T2H_NUMBER_SECTIONS
            $T2H_NODE_NAME_IN_MENU
            $T2H_AVOID_MENU_REDUNDANCY
            $T2H_SECTION_NAVIGATION
            $T2H_SHORTEXTN 
            $T2H_OUT 
            $T2H_DEF_TABLE 
            $T2H_LANG 
            $T2H_L2H 
            $T2H_L2H_L2H 
            $T2H_L2H_SKIP 
            $T2H_L2H_TMP 
            $T2H_L2H_CLEAN 
            @T2H_INCLUDE_DIRS 

            $T2H_CENTER_IMAGE
            $T2H_EXAMPLE_INDENT_CELL
            $T2H_SMALL_EXAMPLE_INDENT_CELL
            $T2H_SMALL_FONT_SIZE
            $T2H_TOP_HEADING
            $T2H_INDEX_CHAPTER
            $T2H_SPLIT_INDEX
            $T2H_HREF_DIR_INSTEAD_FILE
            $T2H_AFTER_BODY_OPEN
            $T2H_PRE_BODY_CLOSE
            $T2H_EXTRA_HEAD
            $T2H_VERTICAL_HEAD_NAVIGATION
            $T2H_WORDS_IN_PAGE
            $T2H_ICONS
            %T2H_ACTIVE_ICONS
            %T2H_NAVIGATION_TEXT
            %T2H_PASSIVE_ICONS
            %T2H_BUTTONS_GOTO
            @T2H_CHAPTER_BUTTONS
            @T2H_MISC_BUTTONS
            @T2H_SECTION_BUTTONS

            $T2H_ADDRESS
            $T2H_BODYTEXT

            $T2H_print_section
            $T2H_print_Top_header
            $T2H_print_Top_footer
            $T2H_print_Top
            $T2H_print_Toc
            $T2H_print_Overview
            $T2H_print_Footnotes
            $T2H_print_About
            $T2H_print_misc_header
            $T2H_print_misc_footer
            $T2H_print_misc
            $T2H_print_chapter_header
            $T2H_print_chapter_footer
            $T2H_print_page_head
            $T2H_print_page_foot
            $T2H_print_head_navigation
            $T2H_print_foot_navigation
            $T2H_button_icon_img
            $T2H_print_navigation
            $T2H_about_body
            $T2H_print_frame
            $T2H_print_toc_frame
            $T2H_PRE_ABOUT
            $T2H_AFTER_ABOUT

            $T2H_OVERVIEW
            $T2H_THIS_SECTION
            %T2H_HREF
            %T2H_NAME
            %T2H_NODE
            %T2H_THISDOC

            %value
            %user_sub

            $complex_format_map
            $index_properties
            %accent_map
            %def_map
            %format_map
            %predefined_index
            %simple_map
            %style_map
            %things_map
            %to_skip
            %valid_index
            %sec2level

            %l2h_img
           );

#++##############################################################################
#
# NOTE FOR DEBUGGING THIS SCRIPT:
# You can run 'perl texi2html.pl' directly, provided you have
# the environment variable T2H_HOME set to the directory containing
# the texi2html.init file
#
#--##############################################################################

# CVS version:
# $Id: texi2html.pl,v 1.32 2003/03/06 18:54:37 pertusus Exp $

# Homepage:
my $T2H_HOMEPAGE = "http://texi2html.cvshome.org/";

# Authors:
my $T2H_AUTHORS = <<EOT;
Written by: Lionel Cons <Lionel.Cons\@cern.ch> (original author)
            Karl Berry  <karl\@freefriends.org>
            Olaf Bachmann <obachman\@mathematik.uni-kl.de>
            and many others.
Maintained by: Many creative people <dev\@texi2html.cvshome.org>
Send bugs and suggestions to <users\@texi2html.cvshome.org>
EOT

# Version: set in configure.in
my $THISVERSION = '@T2H_VERSION@';
my $THISPROG = "texi2html $THISVERSION"; # program name and version

my $T2H_TODAY; # date set by pretty_date

# The man page for this program is included at the end of this file and can be
# viewed using the command 'nroff -man texi2html'.

#+++############################################################################
#                                                                              #
# Initialization                                                               #
# Pasted content of File $(srcdir)/texi2html.init: Default initializations     #
#                                                                              #
#---############################################################################

# leave this within comments, and keep the require statement
# This way, you can directly run texi2html.pl, if $ENV{T2H_HOME}/texi2html.init
# exists.

# @INIT@

require "$ENV{T2H_HOME}/texi2html.init"
    if ($0 =~ /\.pl$/ &&
        -e "$ENV{T2H_HOME}/texi2html.init" && -r "$ENV{T2H_HOME}/texi2html.init");

#+++############################################################################
#                                                                              #
# Initialization                                                               #
# Pasted content of File $(srcdir)/MySimple.pm: Command-line processing        #
#                                                                              #
#---############################################################################

# leave this within comments, and keep the require statement
# This way, you can directly run texi2html.pl, if $ENV{T2H_HOME}/texi2html.init
# exists.

# @MYSIMPLE@

require "$ENV{T2H_HOME}/MySimple.pm"
    if ($0 =~ /\.pl$/ &&
        -e "$ENV{T2H_HOME}/MySimple.pm" && -r "$ENV{T2H_HOME}/MySimple.pm");

#+++############################################################################
#                                                                              #
# Initialization                                                               #
# Pasted content of File $(srcdir)/T2h_i18n.pm: Internationalisation           #
#                                                                              #
#---############################################################################

# leave this within comments, and keep the require statement
# This way, you can directly run texi2html.pl, if $ENV{T2H_HOME}/texi2html.init
# exists.

# @T2H_I18N@
require "$ENV{T2H_HOME}/T2h_i18n.pm"
    if ($0 =~ /\.pl$/ &&
        -e "$ENV{T2H_HOME}/T2h_i18n.pm" && -r "$ENV{T2H_HOME}/T2h_i18n.pm");

package main;

#+++############################################################################
#                                                                              #
# Constants                                                                    #
#                                                                              #
#---############################################################################

my $DEBUG_TOC   =  1;
my $DEBUG_INDEX =  2;
my $DEBUG_BIB   =  4;
my $DEBUG_GLOSS =  8;
my $DEBUG_DEF   = 16;
my $DEBUG_HTML  = 32;
my $DEBUG_USER  = 64;
my $DEBUG_L2H   = 128;


my $BIBRE = '\[[\w\/-]+\]';        # RE for a bibliography reference
my $FILERE = '[\/\w.+-]+';         # RE for a file name
my $VARRE = '[^\s\{\}]+';          # RE for a variable name
my $NODERE = '[^:]+';             # RE for a node name
my $NODESRE = '[^:]+';             # RE for a list of node names

my $ERROR = "***";                 # prefix for errors
my $WARN  = "**";                  # prefix for warnings

                                # program home page
my $CHAPTEREND = "<!-- End chapter -->\n"; # to know where a chpater ends
my $SECTIONEND = "<!-- End section -->\n"; # to know where section ends
my $TOPEND     = "<!-- End top     -->\n"; # to know where top ends


#
# pre-defined indices
#
$index_properties =
{
 'c' => { name => 'cp'},
 'f' => { name => 'fn', code => 1},
 'v' => { name => 'vr', code => 1},
 'k' => { name => 'ky', code => 1},
 'p' => { name => 'pg', code => 1},
 't' => { name => 'tp', code => 1}
};


%predefined_index = (
                     'cp', 'c',
                     'fn', 'f',
                     'vr', 'v',
                     'ky', 'k',
                     'pg', 'p',
                     'tp', 't',
	            );

#
# valid indices
#
%valid_index = (
                'c', 1,
                'f', 1,
                'v', 1,
                'k', 1,
                'p', 1,
                't', 1,
               );

#
# texinfo section names to level
#
%sec2level = (
	      'top', 0,
	      'chapter', 1,
	      'unnumbered', 1,
	      'majorheading', 1,
	      'chapheading', 1,
	      'appendix', 1,
	      'section', 2,
	      'unnumberedsec', 2,
	      'heading', 2,
	      'appendixsec', 2,
	      'appendixsection', 2,
	      'subsection', 3,
	      'unnumberedsubsec', 3,
	      'subheading', 3,
	      'appendixsubsec', 3,
	      'subsubsection', 4,
	      'unnumberedsubsubsec', 4,
	      'subsubheading', 4,
	      'appendixsubsubsec', 4,
             );

#
# accent map, TeX command to ISO name
#
%accent_map = (
	       '"',  'uml',
	       '~',  'tilde',
	       '^',  'circ',
	       '`',  'grave',
	       '\'', 'acute',
              );

#
# texinfo "simple things" (@foo) to HTML ones
#
%simple_map = (
	       # cf. makeinfo.c
	       "*", "<br>",     # HTML+
	       " ", "&nbsp;",
	       "\t", "&nbsp;",
  	       "-", "&#173;",	 # soft hyphen
	       "\n", "&nbsp;",
	       "|", "",
#	       'tab', '<\/td><td>',
	       # spacing commands
	       ":", "",
	       "!", "!",
	       "?", "?",
	       ".", ".",
	       "-", "",
               # overbar accent
               "=", "=",  
              );

#
# texinfo "things" (@foo{}) to HTML ones
#
%things_map = (
	       'TeX', 'TeX',
	       'br', '<p>',     # paragraph break
	       'bullet', '*',
               #'copyright', '(C)',
	       'copyright', '&copy;',
	       'dots', '<small>...<\/small>',
	       'enddots', '<small>....<\/small>',
	       'equiv', '==',
	       'error', 'error-->',
	       'expansion', '==>',
	       'minus', '-',
	       'point', '-!-',
	       'print', '-|',
	       'result', '=>',
               # APA: &pretty_date requires $MONTH_NAMES and $T2H_LANG
               # to be initialized.  The latter gets initialized by
               # &SetDocumentLanguage in &main.
               # We set following hash entry in &main afterwards.
	       # 'today', &pretty_date,
	       'aa', '&aring;',
	       'AA', '&Aring;',
	       'ae', '&aelig;',
	       'oe', '&#156;',
	       'AE', '&AElig;',
	       'OE', '&#140;',
	       'o',  '&oslash;',
	       'O',  '&Oslash;',
	       'ss', '&szlig;',
	       'l', '\/l',
	       'L', '\/L',
	       'exclamdown', '&iexcl;',
	       'questiondown', '&iquest;',
	       'pounds', '&pound;'
              );

#
# texinfo styles (@foo{bar}) to HTML ones
#
%style_map = (
	      'acronym',    '&do_acronym',
	      'asis',       '',
	      'bullet',      '&do_bullet',
	      'b',          'b',
	      'cite',       'cite',
	      'code',       'code',
	      'command',    'code',
	      'ctrl',       '&do_ctrl',   # special case
	      'dfn',        'em',         # DFN tag is illegal in the standard
	      'dmn',        '',           # useless
	      'email',      '&do_email',  # insert a clickable email address
	      'emph',       'em',
	      'env',        'code',
	      'file',       '"tt',        # will put quotes, cf. &apply_style
	      'i',          'i',
	      'kbd',        'kbd',
	      'key',        'kbd',
	      'math',       '&do_math',
	      'option',     '"samp',      # will put quotes, cf. &apply_style
	      'r',          '',           # unsupported
	      'samp',       '"samp',      # will put quotes, cf. &apply_style
	      'sc',         '&do_sc',     # special case
	      'strong',     'strong',
	      't',          'tt',
	      'titlefont',  '',           # useless
	      'uref',       '&do_uref',   # insert a clickable URL
	      'url',        '&do_url',    # insert a clickable URL
	      'var',        'var',
              'verb',       'tt',
	      'w',          '',           # unsupported
	      'H',          '&do_accent',
	      'dotaccent',  '&do_accent',
	      'ringaccent', '&do_accent',
	      'tieaccent',  '&do_accent',
	      'u',          '&do_accent',
	      'ubaraccent', '&do_accent',
	      'udotaccent', '&do_accent',
	      'v',          '&do_accent',
	      ',',          '&do_accent',
	      'dotless',    '&do_accent'
             );

foreach my $accent (keys(%accent_map))
{
    $style_map{$accent} = '&do_accent';
}

#
# texinfo format (@foo/@end foo) to HTML ones
#
%format_map = (
	       'quotation',  'blockquote',
	       # lists
	       'itemize',    'ul',
	       'enumerate',  'ol',
	       # poorly supported 
               # pertusus: FIXME it enters pre but doesn't flush anything ?
	       'flushleft',  'pre',
	       'flushright', 'pre',
              );

#
# an eval of these $complex_format_map->{what}->[0] yields beginning
# an eval of these $complex_format_map->{what}->[1] yieleds end
$complex_format_map =
{
 example =>
 [
  q{"<table><tr>$T2H_EXAMPLE_INDENT_CELL<td class=\"example\"><pre>"},
  q{'</pre></td></tr></table>'}
 ],
 smallexample =>
 [
  q{"<table><tr>$T2H_SMALL_EXAMPLE_INDENT_CELL<td class=\"smallexample\"><pre><font size=\"$T2H_SMALL_FONT_SIZE\">"},
  q{'</font></pre></td></tr></table>'}
 ],
 display =>
 [
  q{"<table><tr>$T2H_EXAMPLE_INDENT_CELL<td class=\"display\"><pre style=\"font-family: serif\">"},
  q{'</pre></td></tr></table>'}
 ],
 smalldisplay =>
 [
  q{"<table><tr>$T2H_SMALL_EXAMPLE_INDENT_CELL<td class=\"smalldisplay\"><pre style=\"font-family: serif\"><font size=\"$T2H_SMALL_FONT_SIZE\">"},
  q{'</font></pre></td></tr></table>'}
 ]
};

$complex_format_map->{lisp} = $complex_format_map->{example};
$complex_format_map->{smalllisp} = $complex_format_map->{smallexample};
$complex_format_map->{format} = $complex_format_map->{display};
$complex_format_map->{smallformat} = $complex_format_map->{smalldisplay};

#
# texinfo definition shortcuts to real ones
#
%def_map = (
	    # basic commands
	    'deffn', 0,
	    'defvr', 0,
	    'deftypefn', 0,
	    'deftypeop', 0,
	    'deftypevr', 0,
	    'defcv', 0,
	    'defop', 0,
	    'deftp', 0,
	    # basic x commands
	    'deffnx', 0,
	    'defvrx', 0,
	    'deftypefnx', 0,
	    'deftypeopx', 0,
	    'deftypevrx', 0,
	    'defcvx', 0,
	    'defopx', 0,
	    'deftpx', 0,
	    # shortcuts
	    'defun', 'deffn Function',
	    'defmac', 'deffn Macro',
	    'defspec', 'deffn {Special Form}',
	    'defvar', 'defvr Variable',
	    'defopt', 'defvr {User Option}',
	    'deftypefun', 'deftypefn Function',
	    'deftypevar', 'deftypevr Variable',
	    'defivar', 'defcv {Instance Variable}',
	    'deftypeivar', 'defcv {Instance Variable}', # NEW: FIXME
	    'defmethod', 'defop Method',
	    'deftypemethod', 'defop Method', # NEW:FIXME
	    # x shortcuts
	    'defunx', 'deffnx Function',
	    'defmacx', 'deffnx Macro',
	    'defspecx', 'deffnx {Special Form}',
	    'defvarx', 'defvrx Variable',
	    'defoptx', 'defvrx {User Option}',
	    'deftypefunx', 'deftypefnx Function',
	    'deftypevarx', 'deftypevrx Variable',
	    'defivarx', 'defcvx {Instance Variable}',
	    'defmethodx', 'defopx Method',
           );

#
# things to skip
#
%to_skip = (
	    # comments
#	    'c', 1,
#	    'comment', 1,
#            'ifnotinfo', 1,
#            'ifnottex', 1,
#	    'ifhtml', 1,
#	    'end ifhtml', 1,
#            'end ifnotinfo', 1,
	    #           'end ifnottex', 1,
	    # useless
	    'detailmenu', 1,
            'end detailmenu', 1,
            'direntry', 1,
            'end direntry', 1,
	    'contents', 1,
	    'shortcontents', 1,
	    'summarycontents', 1,
	    'footnotestyle', 1,
#	    'end ifclear', 1,
#	    'end ifset', 1,
#	    'titlepage', 1,
#	    'end titlepage', 1,
	    # unsupported commands (formatting)
	    'afourpaper', 1,
	    'cropmarks', 1,
	    'finalout', 1,
	    'headings', 1,
            'sp', 1,
	    'need', 1,
	    'page', 1,
	    'setchapternewpage', 1,
	    'everyheading', 1,
	    'everyfooting', 1,
	    'evenheading', 1,
	    'evenfooting', 1,
	    'oddheading', 1,
	    'oddfooting', 1,
	    'smallbook', 1,
	    'vskip', 1,
	    'filbreak', 1,
	    'paragraphindent', 1,
	    # unsupported formats
	    'cartouche', 1,
	    'end cartouche', 1,
	    'group', 1,
	    'end group', 1,
           );

# raw formats which are not be expanded
my @raw_text = ('tex', 'html', 'verbatim');

# text macros ignored based on a value
my @value_text = ('ifclear', 'ifset' );

# all the potentially ignorable text macros
my @text_macros = ('iftex', 'ignore', 'ifplaintext', 'ifinfo', 'ifhtml', 
'html', 'tex', 'titlepage', 'ifnothtml', 'ifnottex', 'ifnotplaintext', 
'ifnotinfo', 'verbatim', 'ifclear', 'ifset' );

# text not ignored (titlepage is treated especially)
my @kept_text = ('ifinfo', 'ifhtml', 'html', 'ifnottex', 'ifnotplaintext', 
'ifnotinfo', 'verbatim', 'titlepage');

#+++############################################################################
#                                                                              #
# Argument parsing, initialisation                                             #
#                                                                              #
#---############################################################################

#
# flush stdout and stderr after every write
#
select(STDERR);
$| = 1;
select(STDOUT);
$| = 1;

my $init_file;
#
# called on -init-file
sub LoadInitFile
{
    # First argument is option
    shift;
    # second argument is value of options
    $init_file = shift;
    if (-f $init_file)
    {
        print "# reading initialization file from $init_file\n"
            if ($T2H_VERBOSE);
        require($init_file);
    }
    else
    {
        print "$ERROR Error: can't read init file $init_file\n";
        $init_file = '';
    }
}

my $T2H_WORDS;
#
# called on -lang
sub SetDocumentLanguage
{
    my $lang = shift;
    $T2H_WORDS = T2h_i18n::set_language($lang);
    if (defined($T2H_WORDS))
    {
        print "# using '$lang' as document language\n" if ($T2H_VERBOSE);
        $T2H_LANG = $lang;
    }
    else
    {
        warn "$ERROR: Language specs for '$lang' do not exists. Reverting to '$T2H_LANG'\n";
    }
}

# T2H_OPTIONS is a hash whose keys are the (long) names of valid
# command-line options and whose values are a hash with the following keys:
# type    ==> one of !|=i|:i|=s|:s (see GetOpt::Long for more info)
# linkage ==> ref to scalar, array, or subroutine (see GetOpt::Long for more info)
# verbose ==> short description of option (displayed by -h)
# noHelp  ==> if 1 -> for "not so important options": only print description on -h 1
#                2 -> for obsolete options: only print description on -h 2
my $T2H_OPTIONS;
$T2H_OPTIONS -> {debug} =
{
 type => '=i',
 linkage => \$T2H_DEBUG,
 verbose => 'output HTML with debuging information',
};

$T2H_OPTIONS -> {doctype} =
{
 type => '=s',
 linkage => \$T2H_DOCTYPE,
 verbose => 'document type which is output in header of HTML files',
 noHelp => 1
};

$T2H_OPTIONS -> {frameset_doctype} =
{
 type => '=s',
 linkage => \$T2H_FRAMESET_DOCTYPE,
 verbose => 'document type for HTML frameset documents',
 noHelp => 1
};

$T2H_OPTIONS -> {test} =
{
 type => '!',
 linkage => \$T2H_TEST,
 verbose => 'use predefined information to avoid differences with reference files',
 noHelp => 1
};

$T2H_OPTIONS -> {expand} =
{
 type => '=s',
 linkage => \@T2H_EXPAND,
 verbose => 'Expand info|tex|none section of texinfo source',
};

$T2H_OPTIONS -> {glossary} =
{
 type => '!',
 linkage => \$T2H_USE_GLOSSARY,
 verbose => "if set, uses section named `Footnotes' for glossary",
 noHelp  => 1,
};


$T2H_OPTIONS -> {invisible} =
{
 type => '=s',
 linkage => \$T2H_INVISIBLE_MARK,
 verbose => 'use text in invisble anchot',
 noHelp  => 1,
};

$T2H_OPTIONS -> {iso} =
{
 type => 'iso',
 linkage => \$T2H_USE_ISO,
 verbose => 'if set, ISO8859 characters are used for special symbols (like copyright, etc)',
 noHelp => 1,
};

$T2H_OPTIONS -> {I} =
{
 type => '=s',
 linkage => \@T2H_INCLUDE_DIRS,
 verbose => 'append $s to the @include search path',
};

$T2H_OPTIONS -> {top_file} =
{
 type => '=s',
 linkage => \$T2H_TOP_FILE,
 verbose => 'use $s as top file, instead of <docname>.html',
};


$T2H_OPTIONS -> {toc_file} =
{
 type => '=s',
 linkage => \$T2H_TOC_FILE,
 verbose => 'use $s as ToC file, instead of <docname>_toc.html',
};

$T2H_OPTIONS -> {frames} =
{
 type => '!',
 linkage => \$T2H_FRAMES,
 verbose => 'output files which use HTML 4.0 frames (experimental)',
 noHelp => 1,
};

$T2H_OPTIONS -> {menu} =
{
 type => '!',
 linkage => \$T2H_SHOW_MENU,
 verbose => 'ouput Texinfo menus',
};

$T2H_OPTIONS -> {number} =
{
 type => '!',
 linkage => \$T2H_NUMBER_SECTIONS,
 verbose => 'use numbered sections'
};


$T2H_OPTIONS -> {split} =
{
 type => '=s',
 linkage => \$T2H_SPLIT,
 verbose => 'split document on section|chapter else no splitting',
};

$T2H_OPTIONS -> {sec_nav} =
{
 type => '!',
 linkage => \$T2H_SECTION_NAVIGATION,
 verbose => 'output navigation panels for each section',
};

$T2H_OPTIONS -> {subdir} =
{
 type => '=s',
 linkage => \$T2H_SUBDIR,
 verbose => 'put HTML files in directory $s, instead of $cwd',
};

$T2H_OPTIONS -> {short_ext} =
{
 type => '!',
 linkage => \$T2H_SHORTEXTN,
 verbose => 'use "htm" extension for output HTML files',
};

$T2H_OPTIONS -> {prefix} =
{
 type => '=s',
 linkage => \$T2H_PREFIX,
 verbose => 'use as prefix for output files, instead of <docname>',
};

$T2H_OPTIONS -> {out_file} =
{
 type => '=s',
 linkage => sub {$T2H_OUT = $_[1]; $T2H_SPLIT = '';},
 verbose => 'if set, all HTML output goes into file $s',
};

$T2H_OPTIONS -> {short_ref} =
{
 type => '!',
 linkage => \$T2H_SHORT_REF,
 verbose => 'if set, references are without section numbers',
};

$T2H_OPTIONS -> {idx_sum} =
{
 type => '!',
 linkage => \$T2H_IDX_SUMMARY,
 verbose => 'if set, also output index summary',
 noHelp  => 1,
};

$T2H_OPTIONS -> {def_table} =
{
 type => '!',
 linkage => \$T2H_DEF_TABLE,
 verbose => 'if set, \@def.. are converted using tables.',
 noHelp  => 1,
};

$T2H_OPTIONS -> {Verbose} =
{
 type => '!',
 linkage => \$T2H_VERBOSE,
 verbose => 'print progress info to stdout',
};

$T2H_OPTIONS -> {lang} =
{
 type => '=s',
 linkage => sub {SetDocumentLanguage($_[1])},
 verbose => 'use $s as document language (ISO 639 encoding)',
};

$T2H_OPTIONS -> {l2h} =
{
 type => '!',
 linkage => \$T2H_L2H,
 verbose => 'if set, uses latex2html for @math and @tex',
};

$T2H_OPTIONS -> {l2h_l2h} =
{
 type => '=s',
 linkage => \$T2H_L2H_L2H,
 verbose => 'program to use for latex2html translation',
 noHelp => 1,
};

$T2H_OPTIONS -> {l2h_skip} =
{
 type => '!',
 linkage => \$T2H_L2H_SKIP,
 verbose => 'if set, tries to reuse previously latex2html output',
 noHelp => 1,
};

$T2H_OPTIONS -> {l2h_tmp} =
{
 type => '=s',
 linkage => \$T2H_L2H_TMP,
 verbose => 'if set, uses $s as temporary latex2html directory',
 noHelp => 1,
};

$T2H_OPTIONS -> {l2h_clean} =
{
 type => '!',
 linkage => \$T2H_L2H_CLEAN,
 verbose => 'if set, do not keep intermediate latex2html files for later reuse',
 noHelp => 1,
};

$T2H_OPTIONS -> {D} =
{
 type => '=s',
 linkage => sub {$value{$_[1]} = 1;},
 verbose => 'equivalent to Texinfo "@set $s 1"',
 noHelp => 1,
};

$T2H_OPTIONS -> {init_file} =
{
 type => '=s',
 linkage => \&LoadInitFile,
 verbose => 'load init file $s'
};
##
## obsolete cmd line options
##
my $T2H_OBSOLETE_OPTIONS;
$T2H_OBSOLETE_OPTIONS -> {'no-section_navigation'} =
{
 type => '!',
 linkage => sub {$T2H_SECTION_NAVIGATION = 0;},
 verbose => 'obsolete, use -nosec_nav',
 noHelp => 2,
};
my $use_acc; # not used
$T2H_OBSOLETE_OPTIONS -> {use_acc} =
{
 type => '!',
 linkage => \$use_acc,
 verbose => 'obsolete, set to true unconditionnaly',
 noHelp => 2
};
$T2H_OBSOLETE_OPTIONS -> {expandinfo} =
{
 type => '!',
 linkage => sub {push @T2H_EXPAND, 'info';},
 verbose => 'obsolete, use "-expand info" instead',
 noHelp => 2,
};
$T2H_OBSOLETE_OPTIONS -> {expandtex} =
{
 type => '!',
 linkage => sub {push @T2H_EXPAND, 'tex';},
 verbose => 'obsolete, use "-expand tex" instead',
 noHelp => 2,
};
$T2H_OBSOLETE_OPTIONS -> {monolithic} =
{
 type => '!',
 linkage => sub {$T2H_SPLIT = '';},
 verbose => 'obsolete, use "-split no" instead',
 noHelp => 2
};
$T2H_OBSOLETE_OPTIONS -> {split_node} =
{
 type => '!',
 linkage => sub{$T2H_SPLIT = 'section';},
 verbose => 'obsolete, use "-split section" instead',
 noHelp => 2,
};
$T2H_OBSOLETE_OPTIONS -> {split_chapter} =
{
 type => '!',
 linkage => sub{$T2H_SPLIT = 'chapter';},
 verbose => 'obsolete, use "-split chapter" instead',
 noHelp => 2,
};
$T2H_OBSOLETE_OPTIONS -> {no_verbose} =
{
 type => '!',
 linkage => sub {$T2H_VERBOSE = 0;},
 verbose => 'obsolete, use -noverbose instead',
 noHelp => 2,
};
$T2H_OBSOLETE_OPTIONS -> {output_file} =
{
 type => '=s',
 linkage => sub {$T2H_OUT = $_[1]; $T2H_SPLIT = '';},
 verbose => 'obsolete, use -out_file instead',
 noHelp => 2
};

$T2H_OBSOLETE_OPTIONS -> {section_navigation} =
{
 type => '!',
 linkage => \$T2H_SECTION_NAVIGATION,
 verbose => 'obsolete, use -sec_nav instead',
 noHelp => 2,
};

$T2H_OBSOLETE_OPTIONS -> {verbose} =
{
 type => '!',
 linkage => \$T2H_VERBOSE,
 verbose => 'obsolete, use -Verbose instead',
 noHelp => 2
};

# read initialzation from $sysconfdir/texi2htmlrc or $HOME/.texi2htmlrc
my $home = $ENV{HOME};
defined($home) or $home = '';
foreach my $i ('@sysconfdir@/texi2htmlrc', "$home/.texi2htmlrc")
{
    if (-f $i)
    {
	print "# reading initialization file from $i\n"
	    if ($T2H_VERBOSE);
	require($i);
    }
}

%value = ();                       # hold texinfo variables, see also -D

#+++############################################################################
#                                                                              #
# parse command-line options
#                                                                              #
#---############################################################################
my $T2H_USAGE_TEXT = <<EOT;
Usage: texi2html  [OPTIONS] TEXINFO-FILE
Translates Texinfo source documentation to HTML.
EOT
my $T2H_FAILURE_TEXT = <<EOT;
Try 'texi2html -help' for usage instructions.
EOT
my $options = new Getopt::MySimple;
# some older version of GetOpt::Long don't have
# Getopt::Long::Configure("pass_through")
eval {Getopt::Long::Configure("pass_through");};
my $Configure_failed = $@ && <<EOT;
**WARNING: Parsing of obsolete command-line options could have failed.
           Consider to use only documented command-line options (run
           'texi2html -help 2' for a complete list) or upgrade to perl
           version 5.005 or higher.
EOT
# FIXME getOptions is called 2 times, and thus adds 2 times the default 
# help and version 
if (! $options->getOptions($T2H_OPTIONS, $T2H_USAGE_TEXT, "$THISVERSION\n"))
{
    print $Configure_failed if $Configure_failed;
    die $T2H_FAILURE_TEXT;
}

if (@ARGV > 1)
{
    eval {Getopt::Long::Configure("no_pass_through");};
    if (! $options->getOptions($T2H_OBSOLETE_OPTIONS, $T2H_USAGE_TEXT, "$THISVERSION\n"))
    {
        print $Configure_failed if $Configure_failed;
        die $T2H_FAILURE_TEXT;
    }
}


#+++############################################################################
#                                                                              #
# evaluation of cmd line options
#                                                                              #
#---############################################################################

foreach my $expanded (@T2H_EXPAND)
{
    push (@kept_text, "if$expanded") unless grep {"if$expanded" eq $_} @kept_text;
    push (@kept_text, "$expanded") unless grep {"$expanded" eq $_} @kept_text;
}

$T2H_INVISIBLE_MARK = '<img src="invisible.xbm" alt="">' if $T2H_INVISIBLE_MARK eq 'xbm';

#
# file name buisness
#

my $docu_dir;            # directory of the document
my $docu_name;           # basename of the document
my $docu_rdir;           # directory for the output
my $docu_ext = "html";   # extension
my $docu_toc;            # document's table of contents
my $docu_stoc;           # document's short toc
my $docu_foot;           # document's footnotes
my $docu_about;          # about this document
my $docu_top;            # document top
my $docu_doc;            # current document

die "Need exactly one file to translate\n$T2H_FAILURE_TEXT" unless @ARGV == 1;
my $docu = shift(@ARGV);
if ($docu =~ /.*\//)
{
    chop($docu_dir = $&);
    $docu_name = $';
}
else
{
    $docu_dir = '.';
    $docu_name = $docu;
}
unshift(@T2H_INCLUDE_DIRS, $docu_dir);
$docu_name =~ s/\.te?x(i|info)?$//;
$docu_name = $T2H_PREFIX if ($T2H_PREFIX);

# subdir
if ($T2H_SUBDIR && ! $T2H_OUT)
{
    $T2H_SUBDIR =~ s|/*$||;
    unless (-d "$T2H_SUBDIR" && -w "$T2H_SUBDIR")
    {
        if ( mkdir($T2H_SUBDIR, oct(755)))
        {
            print "# created directory $T2H_SUBDIR\n" if ($T2H_VERBOSE);
        }
        else
        {
            warn "$ERROR can't create directory $T2H_SUBDIR. Put results into current directory\n";
            $T2H_SUBDIR = '';
        }
    }
}

if ($T2H_SUBDIR && ! $T2H_OUT)
{
    $docu_rdir = "$T2H_SUBDIR/";
    print "# putting result files into directory $docu_rdir\n" if ($T2H_VERBOSE);
}
else
{
    if ($T2H_OUT && $T2H_OUT =~ m|(.*)/|)
    {
        $docu_rdir = "$1/";
        print "# putting result files into directory $docu_rdir\n" if ($T2H_VERBOSE);
    }
    else
    {
        print "# putting result files into current directory \n" if ($T2H_VERBOSE);
        $docu_rdir = '';
    }
}

# extension
if ($T2H_SHORTEXTN)
{
    $docu_ext = "htm";
}
if ($T2H_TOP_FILE =~ s/\..*$//)
{
    $T2H_TOP_FILE .= ".$docu_ext";
}

# result files
if (! $T2H_OUT && ($T2H_SPLIT =~ /section/i || $T2H_SPLIT =~ /node/i))
{
    $T2H_SPLIT = 'section';
}
elsif (! $T2H_OUT && $T2H_SPLIT =~ /chapter/i)
{
    $T2H_SPLIT = 'chapter'
}
else
{
    undef $T2H_SPLIT;
}

$docu_doc = "$docu_name.$docu_ext"; # document's contents
if ($T2H_SPLIT)
{
    $docu_toc   = $T2H_TOC_FILE || "${docu_name}_toc.$docu_ext"; 
    $docu_stoc  = "${docu_name}_ovr.$docu_ext"; 
    $docu_foot  = "${docu_name}_fot.$docu_ext";
    $docu_about = "${docu_name}_abt.$docu_ext";
    $docu_top   = $T2H_TOP_FILE || $docu_doc;
}
else
{
    if ($T2H_OUT)
    {
        $docu_doc = $T2H_OUT;
        $docu_doc =~ s|.*/||;
    }
    $docu_toc = $docu_foot = $docu_stoc = $docu_about = $docu_top = $docu_doc;
}

my $docu_doc_file = "$docu_rdir$docu_doc";
my $docu_toc_file  = "$docu_rdir$docu_toc";
my $docu_stoc_file = "$docu_rdir$docu_stoc";
my $docu_foot_file = "$docu_rdir$docu_foot";
my $docu_about_file = "$docu_rdir$docu_about";
my $docu_top_file  = "$docu_rdir$docu_top";

my $docu_frame_file =     "$docu_rdir${docu_name}_frame.$docu_ext";
my $docu_toc_frame_file = "$docu_rdir${docu_name}_toc_frame.$docu_ext";

#
# variables
#
$value{'html'} = 1;             # predefine html (the output format)
$value{'texi2html'} = $THISVERSION; # predefine texi2html (the translator)
# _foo: internal to track @foo
foreach my $key ('_author', '_title', '_subtitle',
	 '_settitle', '_setfilename', '_shorttitle')
{
    $value{$key} = '';            # prevent -w warnings
}
my %node2sec = ();                 # node to section name
my %sec2node = ();			# section to node name
my %sec2seccount = ();		# section to section count
my %seccount2sec = ();		# section count to section
my %sec2number = ();		# section to number
				# $number =~ ^[\dA-Z]+\.(\d+(\.\d+)*)?$
my %sec2index = ();                # 1 if section in index
my %number2sec = ();		# number to section
my %idx2node = ();                 # index keys to node
my %node2href = ();                # node to HREF
my %node2next = ();                # node to next
my %node2prev = ();                # node to prev
my %node2up = ();                # node to up
my %bib2href = ();                 # bibliography reference to HREF
my %gloss2href = ();               # glossary term to HREF
my @sections = ();                 # list of sections

my $first_index_chapter;
my $index;                         # ref on a hash for the indexes
#
# initial indexes
#
my $bib_num = 0;
my $foot_num = 0;
my $gloss_num = 0;
my $idx_num = 0;
my $sec_num = 0;
my $doc_num = 0;
my $html_num = 0;

#
# can I use ISO8859 characters? (HTML+)
#
if ($T2H_USE_ISO)
{
    $things_map{'bullet'} = "&bull;";
    $things_map{'copyright'} = "&copy;";
    $things_map{'dots'} = "&hellip;";
    $things_map{'equiv'} = "&equiv;";
    $things_map{'expansion'} = "&rarr;";
    $things_map{'point'} = "&lowast;";
    $things_map{'result'} = "&rArr;";
}

#
# read texi2html extensions (if any)
#
my $extensions = 'texi2html.ext';  # extensions in working directory
if (-f $extensions)
{
    print "# reading extensions from $extensions\n" if $T2H_VERBOSE;
    require($extensions);
}
my $progdir;
($progdir = $0) =~ s/[^\/]+$//;
if ($progdir && ($progdir ne './'))
{
    $extensions = "${progdir}texi2html.ext"; # extensions in texi2html directory
    if (-f $extensions)
    {
	print "# reading extensions from $extensions\n" if $T2H_VERBOSE;
	require($extensions);
    }
}

print "# reading from $docu\n" if $T2H_VERBOSE;

#########################################################################
#
# latex2html stuff
#
# latex2html conversions consist of three stages:
# 1) ToLatex: Put "latex" code into a latex file
# 2) ToHtml: Use latex2html to generate corresponding html code and images
# 3) FromHtml: Extract generated code and images from latex2html run
#

# init l2h defaults for files and names

my ($l2h_name, $l2h_latex_file, $l2h_cache_file, $l2h_html_file, $l2h_prefix);

if ($T2H_L2H)
{
    $l2h_name =  "${docu_name}_l2h";
    $l2h_latex_file = "$docu_rdir${l2h_name}.tex";
    $l2h_cache_file = "${docu_rdir}l2h_cache.pm";
    $T2H_L2H_L2H = "latex2html" unless ($T2H_L2H_L2H);
    # destination dir -- generated images are put there, should be the same
    # as dir of enclosing html document --
    $l2h_html_file = "$docu_rdir${l2h_name}.html";
    $l2h_prefix = "${l2h_name}_";
}

##########################
#
# First stage: Generation of Latex file
# Initialize with: l2h_InitToLatex
# Add content with: l2h_ToLatex($text) --> HTML placeholder comment
# Finish with: l2h_FinishToLatex
#

my $l2h_latex_preamble = <<EOT;
% This document was automatically generated by the l2h extenstion of texi2html
% DO NOT EDIT !!!
\\documentclass{article}
\\usepackage{html}
\\begin{document}
EOT

my $l2h_latex_closing = <<EOT;
\\end{document}
EOT

my %l2h_to_latex = ();
my @l2h_to_latex = ();
my $l2h_latex_count = 0;
my $l2h_to_latex_count = 0;
my $l2h_cached_count = 0;
my %l2h_cache = ();
$T2H_L2H = l2h_InitToLatex() if ($T2H_L2H);

# return used latex 1, if l2h could be initalized properly, 0 otherwise
sub l2h_InitToLatex
{
    unless ($T2H_L2H_SKIP)
    {
        unless (open(L2H_LATEX, ">$l2h_latex_file"))
        {
            warn "$ERROR Error l2h: Can't open latex file '$l2h_latex_file' for writing\n";
            return 0;
        }
        print "# l2h: use ${l2h_latex_file} as latex file\n" if ($T2H_VERBOSE);
        print L2H_LATEX $l2h_latex_preamble;
    }
    # open database for caching
    l2h_InitCache();
    return  1;
}


# print text (1st arg) into latex file (if not already there), return
# HTML commentary which can be later on replaced by the latex2html
# generated text
sub l2h_ToLatex
{
    my($text) = @_;
    my($count);
    $l2h_to_latex_count++;
    $text =~ s/(\s*)$//;
    # try whether we can cache it
    my $cached_text = l2h_FromCache($text);
    if ($cached_text)
    {
        $l2h_cached_count++;
        return $cached_text;
    }
    # try whether we have text already on things to do
    unless ($count = $l2h_to_latex{$text})
    {
        $count = $l2h_latex_count;
        $l2h_latex_count++;
        $l2h_to_latex{$text} = $count;
        $l2h_to_latex[$count] = $text;
        unless ($T2H_L2H_SKIP)
        {
            print L2H_LATEX "\\begin{rawhtml}\n";
            print L2H_LATEX "<!-- l2h_begin ${l2h_name} ${count} -->\n";
            print L2H_LATEX "\\end{rawhtml}\n";

            print L2H_LATEX "$text\n";

            print L2H_LATEX "\\begin{rawhtml}\n";
            print L2H_LATEX "<!-- l2h_end ${l2h_name} ${count} -->\n";
            print L2H_LATEX "\\end{rawhtml}\n";
        }
    }
    return "<!-- l2h_replace ${l2h_name} ${count} -->";
}

# print closing into latex file and close it
sub l2h_FinishToLatex
{
    my ($reused);
    $reused = $l2h_to_latex_count - $l2h_latex_count - $l2h_cached_count;
    unless ($T2H_L2H_SKIP)
    {
        print L2H_LATEX $l2h_latex_closing;
        close(L2H_LATEX);
    }
    print "# l2h: finished to latex ($l2h_cached_count cached, $reused reused, $l2h_latex_count contents)\n" if ($T2H_VERBOSE);
    unless ($l2h_latex_count)
    {
        l2h_Finish();
        return 0;
    }
    return 1;
}

###################################
# Second stage: Use latex2html to generate corresponding html code and images
#
# l2h_ToHtml([$l2h_latex_file, [$l2h_html_dir]]):
#   Call latex2html on $l2h_latex_file
#   Put images (prefixed with $l2h_name."_") and html file(s) in $l2h_html_dir
#   Return 1, on success
#          0, otherwise
#
sub l2h_ToHtml
{
    my ($call, $dotbug);
    if ($T2H_L2H_SKIP)
    {
        print "# l2h: skipping latex2html run\n" if ($T2H_VERBOSE);
        return 1;
    }
    # Check for dot in directory where dvips will work
    if ($T2H_L2H_TMP)
    {
        if ($T2H_L2H_TMP =~ /\./)
        {
            warn "$ERROR Warning l2h: l2h_tmp dir contains a dot. Use /tmp, instead\n";
            $dotbug = 1;
        }
    }
    else
    {
        if (getcwd() =~ /\./)
        {
            warn "$ERROR Warning l2h: current dir contains a dot. Use /tmp as l2h_tmp dir \n";
            $dotbug = 1;
        }
    }
    # fix it, if necessary and hope that it works
    $T2H_L2H_TMP = "/tmp" if ($dotbug);

    $call = $T2H_L2H_L2H;
    # use init file, if specified
    $call = $call . " -init_file " . $init_file if ($init_file && -f $init_file);
    # set output dir
    $call .=  ($docu_rdir ? " -dir $docu_rdir" : " -no_subdir");
    # use l2h_tmp, if specified
    $call = $call . " -tmp $T2H_L2H_TMP" if ($T2H_L2H_TMP);
    # options we want to be sure of
    $call = $call ." -address 0 -info 0 -split 0 -no_navigation -no_auto_link";
    $call = $call ." -prefix ${l2h_prefix} $l2h_latex_file";

    print "# l2h: executing '$call'\n" if ($T2H_VERBOSE);
    if (system($call))
    {
        warn "l2h ***Error: '${call}' did not succeed\n";
        return 0;
    }
    else
    {
        print "# l2h: latex2html finished successfully\n" if ($T2H_VERBOSE);
        return 1;
    }
}

# this is directly pasted over from latex2html
sub getcwd
{
    local($_) = `pwd`;

    die "'pwd' failed (out of memory?)\n"
        unless length;
    chop;
    $_;
}


##########################
# Third stage: Extract generated contents from latex2html run
# Initialize with: l2h_InitFromHtml
#   open $l2h_html_file for reading
#   reads in contents into array indexed by numbers
#   return 1,  on success -- 0, otherwise
# Extract Html code with: l2h_FromHtml($text)
#   replaces in $text all previosuly inserted comments by generated html code
#   returns (possibly changed) $text
# Finish with: l2h_FinishFromHtml
#   closes $l2h_html_dir/$l2h_name.".$docu_ext"

my $l2h_extract_error = 0;
my $l2h_range_error = 0;
my @l2h_from_html;

sub l2h_InitFromHtml()
{
    local(%l2h_img);
    my ($count, $h_line);

    if (! open(L2H_HTML, "<${l2h_html_file}"))
    {
        print "$ERROR Error l2h: Can't open ${l2h_html_file} for reading\n";
        return 0;
    }
    print "# l2h: use ${l2h_html_file} as html file\n" if ($T2H_VERBOSE);

    my $l2h_html_count = 0;
    while ($h_line = <L2H_HTML>)
    {
        if ($h_line =~ /^<!-- l2h_begin $l2h_name ([0-9]+) -->/)
        {
            $count = $1;
            my $h_content = "";
            while ($h_line = <L2H_HTML>)
            {
                if ($h_line =~ /^<!-- l2h_end $l2h_name $count -->/)
                {
                    chomp $h_content;
                    chomp $h_content;
                    $l2h_html_count++;
                    $h_content = l2h_ToCache($count, $h_content);
                    $l2h_from_html[$count] = $h_content;
                    $h_content = '';
                    last;
                }
                $h_content = $h_content.$h_line;
            }
            if ($h_content)
            {
                print "$ERROR Warning l2h: l2h_end $l2h_name $count not found\n"
                    if ($T2H_VERBOSE);
                close(L2H_HTML);
                return 0;
            }
        }
    }
    print "# l2h: Got $l2h_html_count of $l2h_latex_count html contents\n"
        if ($T2H_VERBOSE);

    close(L2H_HTML);
    return 1;
}

sub l2h_FromHtml($)
{
    my($text) = @_;
    my($done, $to_do, $count);
    $to_do = $text;
    $done = '';
    while ($to_do =~ /([^\000]*)<!-- l2h_replace $l2h_name ([0-9]+) -->([^\000]*)/)
    {
        $to_do = $1;
        $count = $2;
        $done = $3.$done;
        $done = "<!-- l2h_end $l2h_name $count -->".$done
            if ($T2H_DEBUG & $DEBUG_L2H);

        $done = l2h_ExtractFromHtml($count) . $done;

        $done = "<!-- l2h_begin $l2h_name $count -->".$done
            if ($T2H_DEBUG & $DEBUG_L2H);
    }
    return $to_do.$done;
}


sub l2h_ExtractFromHtml($)
{
    my $count = shift;
    return $l2h_from_html[$count] if ($l2h_from_html[$count]);
    if ($count >= 0 && $count < $l2h_latex_count)
    {
        # now we are in trouble
        my $line;
        $l2h_extract_error++;
        print "$ERROR l2h: can't extract content $count from html\n"
            if ($T2H_VERBOSE);
        # try simple (ordinary) substition (without l2h)
        my $l_l2h = $T2H_L2H;
        $T2H_L2H = 0;
        $line = $l2h_to_latex{$count};
        $line = substitute_style($line);
        $line = unprotect_texi($line);
        $line = "<!-- l2h: ". __LINE__ . " use texi2html -->" . $line
            if ($T2H_DEBUG & $DEBUG_L2H);
        $T2H_L2H = $l_l2h;
        return $line;
    }
    else
    {
        # now we have been incorrectly called
        $l2h_range_error++;
        print "$ERROR l2h: Request of $count content which is out of valide range [0,$l2h_latex_count)\n";
        return "<!-- l2h: ". __LINE__ . " out of range count $count -->"
            if ($T2H_DEBUG & $DEBUG_L2H);
        return "<!-- l2h: out of range count $count -->";
    }
}

sub l2h_FinishFromHtml()
{
    if ($T2H_VERBOSE)
    {
        if ($l2h_extract_error + $l2h_range_error)
        {
            print "# l2h: finished from html ($l2h_extract_error extract and $l2h_range_error errors)\n";
        }
        else
        {
            print "# l2h: finished from html (no errors)\n";
        }
    }
}

sub l2h_Finish()
{
    l2h_StoreCache();
    if ($T2H_L2H_CLEAN)
    {
        local ($_);
        print "# l2h: removing temporary files generated by l2h extension\n"
            if $T2H_VERBOSE;
        while (<"$docu_rdir$l2h_name"*>)
        {
            unlink $_;
        }
    }
    print "# l2h: Finished\n" if $T2H_VERBOSE;
    return 1;
}

##############################
# stuff for l2h caching
#

# I tried doing this with a dbm data base, but it did not store all
# keys/values. Hence, I did as latex2html does it
sub l2h_InitCache
{
    if (-r "$l2h_cache_file")
    {
        my $rdo = do "$l2h_cache_file";
        warn("$ERROR l2h Error: could not load $docu_rdir$l2h_cache_file: $@\n")
            unless ($rdo);
    }
}

sub l2h_StoreCache
{
    return unless $l2h_latex_count;
    my ($key, $value);
    open(FH, ">$l2h_cache_file") || return warn"$ERROR l2h Error: could not open $docu_rdir$l2h_cache_file for writing: $!\n";
    while (($key, $value) = each %l2h_cache)
    {
        # escape stuff
        $key =~ s|/|\\/|g;
        $key =~ s|\\\\/|\\/|g;
        # weird, a \ at the end of the key results in an error
        # maybe this also broke the dbm database stuff
        $key =~ s|\\$|\\\\|;
        $value =~ s/\|/\\\|/go;
        $value =~ s/\\\\\|/\\\|/go;
        $value =~ s|\\\\|\\\\\\\\|g;
        print FH "\n\$l2h_cache_key = q/$key/;\n";
        print FH "\$l2h_cache{\$l2h_cache_key} = q|$value|;\n";
    }
    print FH "1;";
    close(FH);
}

# return cached html, if it exists for text, and if all pictures
# are there, as well
sub l2h_FromCache
{
    my $text = shift;
    my $cached = $l2h_cache{$text};
    if ($cached)
    {
        while ($cached =~ m/SRC="(.*?)"/g)
        {
            unless (-e "$docu_rdir$1")
            {
                return undef;
            }
        }
        return $cached;
    }
    return undef;
}

# insert generated html into cache, move away images,
# return transformed html
my $maximage = 1;
sub l2h_ToCache($$)
{
    my $count = shift;
    my $content = shift;
    my @images = ($content =~ /SRC="(.*?)"/g);
    my ($src, $dest);

    for $src (@images)
    {
        $dest = $l2h_img{$src};
        unless ($dest)
        {
            my $ext;
            if ($src =~ /.*\.(.*)$/ && $1 ne $docu_ext)
            {
                $ext = $1;
            }
            else
            {
                warn "$ERROR: L2h image $src has invalid extension\n";
                next;
            }
            while (-e "$docu_rdir${docu_name}_$maximage.$ext")
            {
                $maximage++;
            }
            $dest = "${docu_name}_$maximage.$ext";
            system("cp -f $docu_rdir$src $docu_rdir$dest");
            $l2h_img{$src} = $dest;
            unlink "$docu_rdir$src" unless ($DEBUG & $DEBUG_L2H);
        }
        $content =~ s/$src/$dest/g;
    }
    $l2h_cache{$l2h_to_latex[$count]} = $content;
    return $content;
}

#+++############################################################################
#                                                                              #
# Pass 0: read source, handle variable, ignored text                           #
#                                                                              #
#---############################################################################

my $has_top = 0;            # did I see a top node?
my $has_top_command = 0;    # did I see @top for automatic pointers?

my @fhs = ();			# hold the file handles to read
my @input_spool = ();		# spooled lines to read
my $fh_name = 'FH000';

my @lines = ();             # whole document


sub pass0
{
    my $first_line = 1;         # is it the first line
    my $in_top = 0;             # am I inside the top node
    my $macros;                 # macros. reference on a hash
    my $state;
    $state->{'complex_format'} = 0; 

 INPUT_LINE: while ($_ = next_line())
    {
        #
        # remove \input on the first lines only
        #
        if ($first_line)
        {
            next if /^\\input/;
            $first_line = 0;
        }
	
        my $tag = '';
        my $end_tag = '';
        if (/^\s*\@end\s+(\w+)\b/)
        {
            $end_tag = $1;
        }
        elsif (/^\s*\@(\w+)\b/)
        {
            $tag = $1;
        }
	
        #############################################################
        # value substitution before macro expansion, so that
        # it works in macro arguments
	# FIXME pertusus: is it really that way and not the other way round ?
        s/\@value{($VARRE)}/$value{$1}/eg;

        #############################################################
        # macro substitution
        while (/\@(\w+)/g)
        {
            if (exists($macros->{$1}))
            {
                my $before = $`;
                my $name = $1;
                my $after = $';
                my @args;
                my $args;
                #####################################################
                # Support for multi-line macro invocations and nested
                # '{' and '}' within macro invocations added by
                # Eric Sunshine <sunshine@sunshineco.com> 2000/09/10.
                #####################################################
                if ($after =~ /^\s*\{/) # Macro arguments delimited by '{' and '}'?
                {
                    my ($protect, $start, $end, $depth, $c) = (0, 0, 0, 0, 0);
                    foreach $c (unpack('C*', $after))
                    {
                        if ($protect)
                        {       # Character protected by '\' or '@'; pass through unmolested.
                            $protect = 0;
                        }
                        elsif ($c == ord('\\') || $c == ord('@'))
                        {       # '\' and '@' remove special meaning of next character.
                            $protect = 1;
                        }
                        elsif ($c == ord('{')) # Allow '{' and '}' to nest.
                        {
                            $depth++;
                        }
                        elsif ($c == ord('}'))
                        {
                            $depth--;
                            last if $depth == 0;
                        }
                        $start++ if !$depth; # Position of opening brace.
                        $end++; # Position of closing brace.
                    }

                    # '{' & '}' did not completely unnest; append next line; try again.
                    if ($depth > 0)
                    {
                        my $paste = next_line();
                        die "$ERROR Missing closing brace '}' for invocation of macro " .
                            "\"\@$name\" on line:\n", substr($_,0,70), "...\n" unless $paste;
                        s/\n$/ /;
                        unshift @input_spool, $_ . $paste;
                        next INPUT_LINE;
                    }

                    # Extract macro arguments from within '{' and '}'.
                    my $len = $end - $start - 1;
                    $args = ($len > 0) ? substr($after, $start + 1, $len) : '';
                    $after = substr($after, $end + 1);
                }
                ############ End Sunshine Modifications #############
                elsif (@{$macros->{$name}->{Args}} == 1) # Macro arg extends to EOL.
                {
                    $args = $after;
                    $args =~ s/^\s*//;
                    $args =~ s/\s*$//;
                    $after = '';
                }
                $args =~ s|\\\\|\\|g;
                $args =~ s|\\{|{|g;
                $args =~ s|\\}|}|g;
                if (@{$macros->{$name}->{Args}} > 1)
                {
                    $args =~ s/(^|[^\\]),/$1$;/g ;
                    $args =~ s|\\,|,|g;
                    @args = split(/$;\s*/, $args) if (@{$macros->{$name}->{Args}} > 1);
                }
                else
                {
                    $args =~ s|\\,|,|g;
                    @args = ($args);
                }
                my $macrobody = $macros->{$name}->{Body};
                for (my $i=0; $i<=$#args; $i++)
                {
                    $macrobody =~ s|\\$macros->{$name}->{Args}->[$i]\\|$args[$i]|g;
                }
                $macrobody =~ s|\\\\|\\|g;
                $_ = $before . $macrobody . $after;
                unshift @input_spool, map {$_ = $_."\n"} split(/\n/, $_);
                next INPUT_LINE;
            }
        }
        if ($tag eq 'macro' || $tag eq 'rmacro')
        {
            if (/^\@$tag\s*(\w+)\s*(.*)/)
            {
                my $name = $1;
                my @args;
                @args = split(/\s*,\s*/ , $1)
                    if ($2 =~ /^\s*{(.*)}\s*/);
                $macros->{$name}->{Args} = \@args;
                $macros->{$name}->{Body} = '';
                while (($_ = next_line()) && $_ !~ /\@end $tag/)
                {
                    $macros->{$name}->{Body} .= $_;
                }
                die "ERROR: No closing '\@end $tag' found for macro definition of '$name'\n"
                    unless (/\@end $tag/);
                chomp $macros->{$name}->{Body};
            }
            else
            {
                warn "$ERROR: Bad macro defintion $_"
            }
            next;
        }
	unless ($state->{'verb'} or $state->{'raw'} or $state->{'ignored'})
        {
        # track variables
            ($value{$1} = $2, next) if /^\@set\s+($VARRE)\s+(.*)$/o;
            (delete $value{$1}, next) if /^\@clear\s+($VARRE)\s*$/o;
            # store things
            ($value{'_shorttitle'} = substitute_text(1, $1), next) if /^\@shorttitle\s+(.*)$/;
            ($value{'_setfilename'} = substitute_text(1, $1), next) if /^\@setfilename\s+(.*)$/;
            ($value{'_settitle'}  = substitute_text(1, $1), next) if /^\@settitle\s+(.*)$/;
            #FIXME pertusus: there might be more than one value for the 
            #author macro
            ($value{'_author'}   .= substitute_text(1, $1)."\n", next) if /^\@author\s+(.*)$/;
            ($value{'_subtitle'} .= substitute_text(1, $1)."\n", next) if /^\@subtitle\s+(.*)$/;
            ($value{'_title'}    .= substitute_text(1, $1)."\n", next) if /^\@title\s+(.*)$/;
        }	
        $_ = scan_line0 ($_, $state);
        unless (defined($_))
        {
            next;
        }
        if ($state->{'raw'})
        {
            push @lines, $_;
            next;
        }
        #remove empty lines
        if ($state->{'verb'})
        {
            if (/^\s*$/ and !$state->{'complex_format'})
            {
                 next if ($state->{'empty_line'});
                 $state->{'empty_line'} = 1;
            }
            else
            {
                 $state->{'empty_line'} = 0;
            }
            push @lines, $_;
            next;
        }
        
        #
        # analyze the tag
        #
        if ($tag)
        {
            last if $tag eq 'bye';
            # special cases
            # APA: Fixed regexp to ONLY match the top node, not any
            # node starting with the word top.
            if ($tag eq 'top' || ($tag eq 'node' && /^\@node\s+top\s*(,.*)?$/i))
            {
                $has_top = 1;
                $has_top_command = 1 if $tag eq 'top';
            }
            elsif ($tag eq 'include')
            {
                if (/^\@include\s+($FILERE)\s*$/o)
                {
                    my $file = LocateIncludeFile($1);
                    if ($file && -e $file)
                    {
                        open_file($file);
                        print "# including $file\n" if $T2H_VERBOSE;
                    }
                    else
                    {
                        warn "$ERROR Can't find $1, skipping";
                    }
                }
                else
                {
                    warn "$ERROR Bad include line: $_";
                }
                next;
            }
            elsif ($tag eq 'synindex' || $tag eq 'syncodeindex')
            {
                if (/^\@$tag\s+(\w+)\s+(\w+)\s*$/)
                {
                    my $from = $1;
                    my $to = $2;
                    my $prefix_from = IndexName2Prefix($from);
                    my $prefix_to = IndexName2Prefix($to);

                    warn("$ERROR unknown from index name $from ind syn*index line: $_"), next
                        unless $prefix_from;
                    warn("$ERROR unknown to index name $to ind syn*index line: $_"), next
                        unless $prefix_to;

                    if ($tag eq 'syncodeindex')
                    {
                        $index_properties->{$prefix_to}->{'from_code'}->{$prefix_from} = 1;
                    }
                    else
                    {
                        $index_properties->{$prefix_to}->{'from'}->{$prefix_from} = 1;
                    }
                }
                else
                {
                    warn "$ERROR Bad syn*index line: $_";
                }
                next;
            }
            elsif ($tag eq 'defindex' || $tag eq 'defcodeindex')
            {
                if (/^\@$tag\s+(\w+)\s*$/)
                {
                    my $name = $1;
                    $index_properties->{$name}->{name} = $name;
                    $index_properties->{$name}->{code} = 1 if $tag eq 'defcodeindex';
                }
                else
                {
                    warn "$ERROR Bad defindex line: $_";
                }
                next;
            }
            elsif ($tag eq 'unmacro')
            {
                delete $macros->{$1} if (/^\@unmacro\s*(\w+)/);
                next;
            }
            elsif ($tag eq 'documentlanguage')
            {
                SetDocumentLanguage($1) if (!$T2H_WORDS && /documentlanguage\s*(\w+)/);
                next;
            }
        }
        next if ($state->{'titlepage'});
        if (/^\s*$/ and !$state->{'complex_format'})
        {
             next if ($state->{'empty_line'});
             $state->{'empty_line'} = 1;
        }
        else
        {
             $state->{'empty_line'} = 0;
        }
        push (@lines, $_);
    }
    my $not_closed = '';
    if ($state->{'verb'})
    {
        $not_closed .= " verb";
        $lines[$#lines] .= "$state->{'verb'}" . '}';
    }
    if ($state->{'raw'})
    {
        $not_closed = " $state->{'raw'}";
        $lines[$#lines] .= " end $state->{'raw'}";
    }
    $not_closed = " @{$state->{'text_macro_stack'}}" 
        if (defined($state->{'text_macro_stack'}) and @{$state->{'text_macro_stack'}});
    $not_closed .= " $state->{'complex_format'} complex formats" if $state->{'complex_format'}; 
    warn "$ERROR $not_closed macros not properly closed\n" if ($not_closed);
    print "# end of pass 0\n" if $T2H_VERBOSE;
}

#+++############################################################################
#                                                                              #
# Pass 1: read source, handle command, variable, simple substitution           #
#                                                                              #
#---############################################################################

# build code for simple substitutions
# the maps used (%simple_map and %things_map) MUST be aware of this
# watch out for regexps, / and escaped characters!
sub build_simple_substitutions ()
{    
    my $subst_code = "my \$text = shift;\n";
    foreach my $simple_macro (keys(%simple_map))
    {
	my $re = $simple_macro;
        $re =~ s/(\W)/\\$1/g; # protect regexp chars
        $subst_code .= "\$text =~  s/\\\@$re/$simple_map{$simple_macro}/g;\n";
    }
    foreach my $thing_macro (keys(%things_map))
    {
        $subst_code .= "\$text =~ s/\\\@$thing_macro\\{\\}/$things_map{$thing_macro}/g;\n";
    }
    # first remove dotless command
    $subst_code .= "\$text =~ s/\\\@dotless\\{([a-z])\\}/\${1}/gi;\n";
    # then substitute accentuated characters
    foreach my $accent_macro (keys(%accent_map))
    {
        my $subst_command;
        if ($accent_macro eq "`")
        {
            $subst_command .= "s/$;3";
        }
        elsif ($accent_macro eq "'")
        {
            $subst_command .= "s/$;4";
        }
        elsif ($accent_macro eq '"')
        {
            $subst_command .= "s/$;5";
        }
        else
        {
            $subst_command .= "s/\\\@\\$accent_macro";
        }
        $subst_code .= "\$text =~ " . $subst_command ."([a-z])/&\${1}$accent_map{$accent_macro};/gi;\n";
        $subst_code .= "\$text =~ " . $subst_command ."{([a-z])}/&\${1}$accent_map{$accent_macro};/gi;\n";
    }
    $subst_code .= "return \$text;\n";
    eval("sub simple_substitutions(\$) { $subst_code }");
}

my $has_top_heading = 0;    # did I see content in top ?
my @html_stack = ();        # HTML elements stack
my $html_element = '';      # current HTML element

my @toc_lines = ();            # table of contents
my @stoc_lines = ();           # table of contents

my @lines1 = ();               #whole document after pass1

my @appendix_sec_num; 
my @normal_sec_num;


sub pass1
{
    my @nodes_and_anchors = (); # all nodes and anchors
    my $curlevel = 0;           # current level in TOC
    my $node = '';              # current node name
    my $node_next = '';         # current node next name
    my $node_prev = '';         # current node prev name
    my $node_up = '';           # current node up name
    my $section;                # current section
    my $in_table = 0;           # am I inside a table 
              # holds the command used to format entries for tables ;
              # for multitable, in_table is like: $table_width:$current_column
              # $current_column == 0 if there was no line
    my $table_type = '';        # type of table ('', 'f', 'v', 'multi')
    my @tables = ();            # nested table support
    my $in_bibliography = 0;    # am I inside a bibliography
    my $in_glossary = 0;        # am I inside a glossary
    my $in_top = 0;             # am I inside the top node
    my $in_pre = 0;             # am I inside a preformatted section
    my $in_list = 0;            # am I inside a list
    my $toplevel;
    my $dont_html = 0;          # don't protect HTML on this line

    html_reset();
    build_simple_substitutions();

    while (@lines)
    {
        $_ = shift(@lines);
        if (!defined($_)) {push @lines1, '<!--undefined line in lines-->';}
        # non-@ substitutions cf. texinfmt.el
        #
        # parse texinfo tags
        #
        my $tag = '';
        my $end_tag = '';
        if (/^\s*\@end\s+(\w+)\b/)
        {
            $end_tag = $1;
        }
        elsif (/^\s*\@(\w+)\b/)
        {
            $tag = $1;
        }
        #
        # handle @html / @end html and @verbatim / @end verbatim
        # 
        # as texinfo 4.5
        # verbatim might begin at another position than beginning
        # of line, and end verbatim might too. To end a verbatim section
        # @end verbatim must have exactly one space between end and verbatim
        # things following end verbatim are not ignored.
        #
        # html might begin at another position than beginning
        # of line, but @end html must begin the line, and have
        # exactly one space. Things following end html are ignored.
        # tex and ignore works like html
        #
        # ifnothtml might begin at another position than beginning
        # of line, and @end  ifnothtml might too, there might be more
        # than one space between @end and ifnothtml but nothing more on 
        # the line.
        # @end itemize, @end ftable works like @end ifnothtml.
        # except that @item on the same line than @end vtable doesn't work
        # 
        # text right after the itemize before an item is displayed.
        # @item might be somewhere in a line. 
        # strangely @item on the same line than @end vtable doesn't work
        # there should be nothing else than a command following @itemize...
        #
        # see more examples in formatting directory


        #
        # try to remove inlined comments
        # syntax from tex-mode.el comment-start-skip
        #
	#s/((^|[^\@])(\@\@)*)\@(c( |\{)|comment ).*$/$1/;

        # Sometimes I use @c right at the end of  a line ( to suppress the line feed )
        #    s/((^|[^\@])(\@\@)*)\@c(omment)?$/$1/;
        #     s/((^|[^\@])(\@\@)*)\@c(omment)? .*/$1/;
        #     s/(.*)\@c{.*?}(.*)/$1$2/;
        #     s/(.*)\@comment{.*?}(.*)/$1$2/;
        #     s/^(.*)\@c /$1/;
        #     s/^(.*)\@comment /$1/;

        #
        # try to skip the line
        #
        if ($end_tag)
        {
            next if $to_skip{"end $end_tag"};
        }
        elsif ($tag)
        {
            next if $to_skip{$tag};
            last if $tag eq 'bye';
        }
        if ($in_top)
        {
            # parsing the top node
            if ($tag eq 'node' ||
                ($sec2level{$tag} && $tag !~ /unnumbered/ && $tag !~ /heading/))
            {
                # no more in top
                $in_top = 0;
                push(@lines1, $TOPEND);
            }
        }
        unless ($in_pre)
        {
            s/``/\"/go;
            s/''/\"/go;
            s/([\w ])---([\w ])/$1--$2/g;
        }
        #
        # analyze the tag
        #
        if ($tag)
        {
            # skip lines
	    #(skip_until($tag), next) if $tag eq 'ignore';
	    #(skip_until($tag), next) if $tag eq 'ifnothtml';
	    #if ($tag eq 'ifinfo')
	    #{
	    #    (skip_until($tag), next) unless grep {$_ eq 'info'} @T2H_EXPAND;
	    #}
	    #if ($tag eq 'iftex')
	    #{
	    #    (skip_until($tag), next) unless grep {$_ eq 'tex'} @T2H_EXPAND;
	    #}
	    #if ($tag eq 'tex')
	    #{
                # add to latex2html file
                #if (grep {$_ eq 'tex'} @T2H_EXPAND && $T2H_L2H && ! $in_pre)
		#{
                    # add space to the end -- tex(i2dvi) does this, as well
		    #push(@lines1, l2h_ToLatex(string_until($tag) . " "));
                #}
		#else
		#{
                    #skip_until($tag);
		#}
		#next;
            #}
            # handle special tables
            if ($tag =~ /^(|f|v|multi)table$/)
            {
                $table_type = $1;
                $tag = 'table';
            }
            # special cases
            # APA: Fixed regexp to ONLY match the top node, not any
            # node starting with the word top.
            if ($tag eq 'top' || ($tag eq 'node' && /^\@node\s+top\s*(,.*)?$/i))
            {
                $in_top = 1;
                @lines1 = ();    # ignore all lines before top (title page garbage)
                next;
            }
            elsif ($tag eq 'node')
            {
                if ($in_top)
                {
                    $in_top = 0;
                    push(@lines1, $TOPEND);
                }
                warn "$ERROR Bad node line: $_" unless $_ =~ /^\@node\s$NODESRE$/o;
                # request of "Richard Y. Kim" <ryk@ap.com>
                s/^\@node\s+//;
                # cedilla @,{c} has a , which could be confused with a , between
                # nodes. We protect it before splitting.
                my @nodes = split(/,/, protect_cedilla($_));
                ($node, $node_next, $node_prev, $node_up) = unprotect_cedilla (@nodes);
                if ($node)
                {
                    $node = normalise_node($node);
                    if (grep {$node eq $_} @nodes_and_anchors)
                    {
                        warn "$ERROR Duplicate node found: $node\n"
                    }
                    else
                    {
                        push @nodes_and_anchors, $node;
                    }
                    $node2href{$node} = "$docu_doc#NODE_$node";
                }
                else
                {
                    warn "$ERROR Node is undefined: $_ (eg. \@node NODE-NAME, NEXT, PREVIOUS, UP)";
                }
                if ($node_next)
                {
                    $node_next = normalise_node($node_next);
                }
                if ($node_prev)
                {
                    $node_prev = normalise_node($node_prev);
                }
                if ($node_up)
                {
                    $node_up = normalise_node($node_up);
                }
                push @lines1, html_debug("<a name=\"".'NODE_'.$node."\"></a>\n", __LINE__);
                next;
            }
            elsif ($tag eq 'menu')
            {
                unless ($T2H_SHOW_MENU)
                {
                    skip_until($tag);
                    next;
                }
                html_push_if($tag);
                push(@lines1, html_debug('', __LINE__));
            }
            elsif ($format_map{$tag})
            {
                $in_pre = 1 if $format_map{$tag} eq 'pre';
                html_push_if($format_map{$tag});
                push(@lines1, html_debug('', __LINE__));
                $in_list++ if $format_map{$tag} eq 'ul' || $format_map{$tag} eq 'ol' ;
                #	    push(@lines, &debug("<blockquote>\n", __LINE__))
                #	      if $tag =~ /example/i;
                # Eric Sunshine <sunshine@sunshineco.com>: <pre>blah</pre>
                # looks better than <pre>\nblah</pre> on OmniWeb2 NextStep
                # browser.
                push(@lines1, debug("<$format_map{$tag}>" .
                                    ($in_pre ? '' : "\n"), __LINE__));
                next;
            }
            elsif (exists $complex_format_map->{$tag})
            {
                my $start = eval $complex_format_map->{$tag}->[0];
                # APA: <table> implicitly ends paragraph, so let's do it
                # explicitly to keep our HTML stack in sync.
                if ($start =~ /\A\s*<table>/i)
                {
                    if ($html_element eq 'p')
                    {
                        push (@lines1, debug("</p>\n", __LINE__));
                        html_pop();
                    }
                }
                if ($@)
                {
                    print "$ERROR: eval of complex_format_map->{$tag}->[0] $complex_format_map->{$tag}->[0]: $@";
                    $start = '<pre>'
                }
                $in_pre = 1 if $start =~ /<pre/;
                push(@lines1, html_debug($start. ($in_pre ? '' : "\n"), __LINE__));
                next;
            }
            elsif ($tag eq 'table')
            {
                if ($table_type eq 'multi')
                { 
                    # first find the table width
                    my $table_width = 0;
                    if (s/^\s*\@multitable\s+\@columnfractions\s+//)
                    {
                        my @fractions = split /\s+/;
                        $table_width = $#fractions + 1;
                        while (@fractions)
                        {
                            my $fraction = shift @fractions;
                            unless ($fraction =~ /^(\d*\.\d+)|(\d+)\.?$/)
                            { 
                                warn "$ERROR column fraction not a number: $fraction";
                            }
                        }
                    }
                    elsif (s/^\s*\@multitable\s+\{//)
                    {
                        my $opened_braces = 1;
                        while ($_ !~ /^\s*$/)
                        {
                            if (s/^[^\{\}]*(\{|\})?//)
                            {
                                if (defined($1))
                                {
                                    $opened_braces++ if ($1 eq '{');
                                    $opened_braces-- if ($1 eq '}');
                                }
                                else
                                {
                                    warn "$ERROR garbage in multitable specification";
                                }
                            }
                            $table_width++ if ($opened_braces == 0);
                            if ($opened_braces < 0)
                            {
                                 warn "$ERROR too much '}' in multitable specification";
                                 $opened_braces = 0;
                            }
                        }
                        if ($opened_braces)
                        {
                            warn "$ERROR '{' not closed in multitable specification";
                            $table_width++;
                        }
                    }
                    $in_table = "$table_width:0";
                    unshift(@tables, join($;, $table_type, $in_table));
                    # APA: <table> implicitly ends paragraph, so let's
                    # do it explicitly to keep our HTML stack in sync.
                    if ($html_element eq 'p')
                    {
                        push (@lines1, debug("</p>\n", __LINE__));
                        html_pop();
                    }
                    # don't use borders -- gets confused by empty cells
                    push(@lines1, debug("<table>\n", __LINE__));
                    html_push_if('table');
                    push(@lines1, html_debug('', __LINE__));
                }
                # anorland@hem2.passagen.se
                # if (/^\s*\@(|f|v|multi)table\s+\@(\w+)|(\{[^\}]*\})/)
                elsif (/^\s*\@(|f|v)table\s+\@(\w+)/)
                {
                    $in_table = $2;
                    unshift(@tables, join($;, $table_type, $in_table));
                    # APA: <dl> implicitly ends paragraph, so let's
                    # do it explicitly to keep our HTML stack in sync.
                    if ($html_element eq 'p')
                    {
                        push (@lines1, debug("</p>\n", __LINE__));
                        html_pop();
                    }
                    push(@lines1, debug("<dl compact=\"compact\">\n", __LINE__));
                    html_push_if('dl');
                    
                    push(@lines1, html_debug('', __LINE__));
                }
                else
                {
                    warn "$ERROR Bad table line: $_";
                }
                next;
            }
            elsif (s/^\@printindex\s+//)
            {
                # APA: HTML generated for @printindex contains <table>
                # which implicitly ends paragraph, so let's do it
                # explicitly to keep our HTML stack in sync.
                if ($html_element eq 'p')
                {
                    push(@lines1, debug("</p>\n", __LINE__));
                    html_pop();
                }
                my $append = '_ ';
                $append = "_$sec_num " if defined($sec_num);
                push (@lines1, "\@printindex" . $append . $_);
                next;
            }
	    #pertusus: also in to_skip, useless and disliked by tidy anyway
	    #elsif ($tag eq 'sp')
	    #{
	    #    push(@lines1, debug("<p></p>\n", __LINE__));
	    #    next;
	    #}
            elsif ($tag eq 'center')
            {
                push(@lines1, debug("<div align=\"center\">\n", __LINE__));
                s/\@center//;
            }
            elsif ($tag eq 'lowersections')
            {
                my ($sec, $level);
                while (($sec, $level) = each %sec2level)
                {
                    $sec2level{$sec} = $level + 1;
                }
                next;
            }
            elsif ($tag eq 'raisesections')
            {
                my ($sec, $level);
                while (($sec, $level) = each %sec2level)
                {
                    $sec2level{$sec} = $level - 1;
                }
                next;
            }
            elsif (defined($def_map{$tag}))
            {
                if ($def_map{$tag})
                {
                    # substitute shortcuts for definition commands
                    s/^\@$tag\s+//;
                    $tag = $def_map{$tag};
                    $_ = "\@$tag $_";
                    $tag =~ s/\s.*//;
                }
            }
            elsif (defined($user_sub{$tag}))
            {
                s/^\@$tag\s+//;
                my $sub = $user_sub{$tag};
                print "# user $tag = $sub, arg: $_" if $T2H_DEBUG & $DEBUG_USER;
                if (defined(&$sub))
                {
                    chop($_);
                    &$sub($_);
                }
                else
                {
                    warn "$ERROR Bad user sub for $tag: $sub\n";
                }
                next;
            }
            if (defined($def_map{$tag}))
            {
                # process definition commands
                s/^\@$tag\s+//;
                my $is_extra = 0;
                if ($tag =~ s/x$//)
                {
                    # extra definition line
                    $is_extra = 1;
                }
                while (/\{([^\{\}]*)\}/)
                {
                    # this is a {} construct
                    my ($before, $contents, $after) = ($`, $1, $');
                    # protect spaces
                    $contents =~ s/\s+/$;9/g;
                    # restore $_ protecting {}
                    $_ = "$before$;7$contents$;8$after";
                }
                my @args = split(/\s+/, &protect_html($_));
                foreach (@args)
                {
                    s/$;9/ /g;  # unprotect spaces
                    s/$;7/\{/g; # ... {
                    s/$;8/\}/g; # ... }
                }
                my $type = shift(@args);
                $type =~ s/^\{(.*)\}$/$1/;
                print "# def ($tag): {$type} ", join(', ', @args), "\n"
                    if $T2H_DEBUG & $DEBUG_DEF;
                $type .= ':' if (!$T2H_DEF_TABLE); # it's nicer like this
                my $name = shift(@args);
                $name =~ s/^\{(.*)\}$/$1/;
                my $ftype;
                if ($is_extra)
                {
                    $_ = debug($T2H_DEF_TABLE ? '' : '<dt>', __LINE__);
                    #$_ = &debug("<tr TEST1>\n", __LINE__) if ($T2H_DEF_TABLE);
                }
                else
                {
                    # APA: <dl> implicitly ends paragraph, so let's
                    # do it explicitly to keep our HTML stack in sync.
                    if ($html_element eq 'p')
                    {
                        $_ = debug("</p>\n", __LINE__);
                        html_pop();
                    }
                    else
                    {
                        $_ = '';
                    }
                    $_ .= debug($T2H_DEF_TABLE ?
                                 "<table width=\"100%\">\n" : "<dl>\n<dt>",
                                 __LINE__);
                }
                if ($tag eq 'deffn' || $tag eq 'defvr' || $tag eq 'deftp')
                {
                    if ($T2H_DEF_TABLE)
                    {
                        $_ .= "<tr>\n<td align=\"left\"><b>$name</b>\n";
                        $_ .= " <i>@args</i>" if @args;
                        $_ .= "</td>\n";
                        $_ .= "<td align=\"right\">";
                        $_ .= "$type</td>\n</tr>\n";
                    }
                    else
                    {
                        $_ .= "<u>$type</u> <b>$name</b>";
                        $_ .= " <i>@args</i>" if @args;
                    }
                }
                elsif ($tag eq 'deftypefn' || $tag eq 'deftypevr'
                       || $tag eq 'deftypeop' || $tag eq 'defcv'
                       || $tag eq 'defop')
                {
                    $ftype = $name;
                    $name = shift(@args);
                    $name =~ s/^\{(.*)\}$/$1/;
                    if ($T2H_DEF_TABLE)
                    {
                        $_ .= "<tr>\n<td align=\"left\"><b>$name</b>";
                        $_ .= " <i>@args</i>" if @args;
                        $_ .= "</td>\n";
                        $_ .= "<td align=\"right\">";
                        $_ .= "$type of $ftype</td>\n</tr>\n";
                    }
                    else
                    {
                        $_ .= "<u>$type</u> $ftype <b>$name</b>";
                        $_ .= " <i>@args</i>" if @args;
                    }
                }
                else
                {
                    warn "$ERROR Unknown definition type: $tag\n";
                    $_ .= "<u>$type</u> <b>$name</b>";
                    $_ .= " <i>@args</i>" if @args;
                }
                $_ .= debug("\n<dd>", __LINE__) if (!$T2H_DEF_TABLE);
                ########$_ .= &debug("\n</table TEST3>\n<table width=\"95%\">\n", __LINE__) if ($T2H_DEF_TABLE);
                $name = unprotect_html($name);
                if ($tag eq 'deffn' || $tag eq 'deftypefn')
                {
                    EnterIndexEntry('f', $name, $docu_doc, $section, \@lines1, $in_pre);
                    #		unshift(@input_spool, "\@findex $name\n");
                }
                elsif ($tag eq 'defop')
                {
                    EnterIndexEntry('f', "$name on $ftype", $docu_doc, $section, \@lines1, $in_pre);
                    #		unshift(@input_spool, "\@findex $name on $ftype\n");
                }
                elsif ($tag eq 'defvr' || $tag eq 'deftypevr' || $tag eq 'defcv')
                {
                    EnterIndexEntry('v', $name, $docu_doc, $section, \@lines1, $in_pre);
                    #		unshift(@input_spool, "\@vindex $name\n");
                }
                else
                {
                    EnterIndexEntry('t', $name, $docu_doc, $section, \@lines1, $in_pre);
                    #		unshift(@input_spool, "\@tindex $name\n");
                }
                $dont_html = 1;
            }
        }
        elsif ($end_tag)
        {
            if ($format_map{$end_tag})
            {
                $in_pre = 0 if $format_map{$end_tag} eq 'pre';
                $in_list-- if $format_map{$end_tag} eq 'ul' || $format_map{$end_tag} eq 'ol' ;
                html_pop_if('p');
                html_pop_if('li');
                html_pop_if();
                push(@lines1, debug("</$format_map{$end_tag}>\n", __LINE__));
                push(@lines1, html_debug('', __LINE__));
            }
            elsif (exists $complex_format_map->{$end_tag})
            {
                my $end = eval $complex_format_map->{$end_tag}->[1];
                if ($@)
                {
                    print "$ERROR: eval of complex_format_map->{$end_tag}->[1] $complex_format_map->{$end_tag}->[0]: $@";
                    $end = '</pre>'
                }
                $in_pre = 0 if $end =~ m|</pre>|;
                push(@lines1, html_debug($end, __LINE__));
            }
            elsif ($end_tag =~ /^(|f|v|multi)table$/)
            {
                unless (@tables)
                {
                    warn "$ERROR \@end $end_tag without \@*table\n";
                    next;
                }
                html_pop_if('p');
                ($table_type, $in_table) = split($;, shift(@tables));
                unless ($1 eq $table_type)
                {
                    warn "$ERROR \@end $end_tag without matching \@$end_tag\n";
                    next;
                }
                if ($table_type eq "multi")
                {
                    my ($max_column, $current_column);
                    ($max_column, $current_column) = split /:/, $in_table;
                    if (!$max_column || !$current_column)
                    { # empty table
                        push(@lines1, "</table>\n");
                    }
                    else
                    { 
                        push(@lines1, "</td></tr></table>\n");
                    }
                    
#                    html_pop_if('tr');
                }
                else
                {
                    # APA: </dl> implicitly ends paragraph, so let's
                    # do it explicitly to keep our HTML stack in sync.
                    if ($html_element eq 'p')
                    {
                        push(@lines1, debug("</p>\n", __LINE__));
                        html_pop();
                    }
                    push(@lines1, "</dl>\n");
                    html_pop_if('dd');
                }
                html_pop_if();
                if (@tables)
                {
                    ($table_type, $in_table) = split($;, $tables[0]);
                }
                else
                {
                    $in_table = 0;
                }
            }
            elsif (defined($def_map{$end_tag}))
            {
                # APA: </dl> and </table> implicitly ends paragraph,
                # so let's do it explicitly to keep our HTML stack in
                # sync.
                if ($html_element eq 'p')
                {
                    push(@lines1, debug("</p>\n", __LINE__));
                    html_pop();
                }
                push(@lines1, debug($T2H_DEF_TABLE ?
                                    "</table>\n" : "</dl>\n", __LINE__));
            }
            elsif ($end_tag eq 'menu')
            {
                html_pop_if();
                push(@lines1, $_); # must keep it for pass 2
            }
            else
            {
                warn "$WARN Unknown \@end $end_tag\n";
                push @lines1, "\@end $end_tag\n";
            }
            next;
        }
        #############################################################
        # anchor insertion
        while (/\@anchor\s*\{(.*?)\}/)
        {
            $_ = $`.$';
            my $anchor = $1;
            $anchor = normalise_node($anchor);
            if (grep {$anchor eq $_} @nodes_and_anchors)
            {
                warn "$ERROR Duplicate node found: $anchor\n"
            }
            else
            {
                push @nodes_and_anchors, $anchor;
            }
            push @lines1, html_debug("<a name=\""."ANC_".$anchor."\"></a>\n", __LINE__);
            $node2href{$anchor} = "$docu_doc#ANC_$anchor";
            next if $_ =~ /^\s*$/;
        }
        #############################################################
        # index entry generation, after value substitutions
        if (/^\@(\w+?)index\s+/)
        {
            EnterIndexEntry($1, $', $docu_doc, $section, \@lines1, $in_pre);
            next;
        }
        #
        # protect texi and HTML things
        $_ = protect_texi($_);
        $_ = protect_html($_) unless $dont_html;
        $dont_html = 0;
        # substitution (unsupported things)
        s/^\@exdent\s+//go;
        s/\@noindent\s+//go;
        s/\@refill\s+//go;
        # other substitutions
        $_ = simple_substitutions($_);
        s/\@footnote\{/\@footnote_$doc_num\{/g; # mark footnotes, cf. do_footnote

        # handle multitables. Transform @tab and @item into html, and
        # ignore if out of bounds.
        if ($in_table && $table_type eq 'multi' && !$end_tag)
        {
            my ($max_column, $current_column);
            ($max_column, $current_column) = split /:/, $in_table;

            next if (!$max_column); # table with 0 columns
            my $stored_text = "";

            while (! /^$/)
            {
                # discard until end of column if out of bounds 
                if ($current_column > $max_column)
                {
                    last unless (s/.*?(\@itemx?\s+)/$1/ or s/.*?(\@itemx?)$/$1/);
                    $current_column = $max_column;
                    next;
                }
                if (/(\@tab\s+|\@itemx?\s+)/ or /(\@tab|\@itemx?)$/)
                {
                    $stored_text = $stored_text . $`;
                    $_ = $';
                    if ($1 =~ /^\@tab/)
                    {
                        $current_column++;
                        if ($current_column == 1)
                        { #first text on first line, first @item was omitted
                             $stored_text = $stored_text . '<tr><td>';
                             $current_column++;
                        }
                        $stored_text = $stored_text . '</td>';

                        if ($current_column > $max_column)
                        {
                            warn "$ERROR too much \@tab";
                        }
                        else
                        {
                            $stored_text = $stored_text . '<td>';
                        }
                    }
                    else # an @item(x) on the line
                    {
                        if ($current_column)
                        {
                             $stored_text = $stored_text . '</td></tr><tr><td>';
                        }
                        else
                        { #first line
                             $stored_text = $stored_text . '<tr><td>';
                        }
                        $current_column = 1;
                    }
                }
                else # no @tab nor @item(x)
                {
                    if (!$current_column)
                    { #first line
                        $stored_text = $stored_text . '<tr><td>';
                        $current_column = 1;
                    }
                    $stored_text = $stored_text . $_;
                    $_ = '';
                }
            }
            $_ = $stored_text;
            $in_table = "$max_column:$current_column";
        }
        #
        # analyze the tag again
        #
        if ($tag)
        {
            if (defined($sec2level{$tag}) && $sec2level{$tag} > 0)
            {
                if (/^\@$tag\s+(.+)$/)
                {
                    my $name = $1;
                    $name = normalise_space_style($name);
                    my $level = $sec2level{$tag};
                    # check for index
                    $first_index_chapter = $name
                        if ($level == 1 && !$first_index_chapter &&
                            $name =~ /index/i);
                    if ($in_top && /heading/)
                    {
                        $has_top_heading = 1;
                        $_ = debug("<h$level>$name</h$level>\n", __LINE__);
                        html_push_if('body');
                        print "# top heading, section $name, level $level\n"
                            if $T2H_DEBUG & $DEBUG_TOC;
                    }
                    else
                    {
                        unless (/^\@\w*heading/)
                        {
                            unless (/^\@unnumbered/)
                            {
                                my $number = update_sec_num($tag, $level);
                                $name = $number . ' ' . $name if $T2H_NUMBER_SECTIONS;
                                $sec2number{$name} = $number;
                                $number2sec{$number} = $name;
                            }
                            if (defined($toplevel))
                            {
                                push @lines1, ($level==$toplevel ? $CHAPTEREND : $SECTIONEND);
                            }
                            else
                            {
                                # first time we see a "section"
                                unless ($level == 1)
                                {
                                    warn "$WARN The first section found is not of level 1: $_";
                                }
                                $toplevel = $level;
                            }
                            push(@sections, $name);
                            next_doc() if (defined $T2H_SPLIT
                                           and
                                           ($T2H_SPLIT eq 'section'
                                            ||
                                            $T2H_SPLIT && $level == $toplevel));
                        }
                        $sec_num++;
                        my $docid = "SEC$sec_num";
                        my $tocid = (/^\@\w*heading/ ? undef : "TOC$sec_num");
                        # check biblio and glossary
                        $in_bibliography = ($name =~ /^([A-Z]|\d+)?(\.\d+)*\s*bibliography$/i);
                        $in_glossary = ($name =~ /^([A-Z]|\d+)?(\.\d+)*\s*glossary$/i);
                        # check node
                        unless ($node)
                        {
                            $name .= ' ' while ($node2sec{$name});
                            $node = $name;
                        }
                        $name .= ' ' while ($sec2node{$name});
                        $section = $name;
                        $node2sec{$node} = substitute_style($name);
                        $sec2node{$name} = $node;
			$sec2seccount{$name} = $sec_num;
			$seccount2sec{$sec_num} = $name;
                        $node2href{$node} = "$docu_doc#$docid";
                        $node2next{$node} = $node_next;
                        $node2prev{$node} = $node_prev;
                        $node2up{$node} = $node_up;
                        print "# node $node, section $name, level $level\n"
                            if $T2H_DEBUG & $DEBUG_TOC;

                        $node = '';
                        $node_next = '';
                        $node_prev = '';
                        $node_next = '';
                        if ($tocid)
                        {
                            my $ul_style = $T2H_NUMBER_SECTIONS ?
                                ' style="list-style: none"' : '';
                            my $ind = '  ' x $curlevel;
                            # update TOC
                            if ($level > $curlevel) {
                                while ($level > $curlevel)
                                {
                                    $curlevel++;
                                    my $ln = "\n$ind<ul${ul_style}>\n";
                                    $ind = '  ' x $curlevel;
                                    push(@toc_lines, $ln);
                                }
                            }
                            elsif ($level < $curlevel)
                            {
                                while ($level < $curlevel)
                                {
                                    $curlevel--;
                                    $ind = '  ' x $curlevel;
                                    push(@toc_lines,
                                         "</li>\n$ind</ul>\n");
                                }
                            }
                            else
                            {
                                push(@toc_lines, "</li>\n");
                            }
                            $_ = t2h_anchor($tocid, "$docu_doc#$docid",$name);
                            $_ = substitute_style($_);
                            push(@stoc_lines,
                                 "  <li>$_</li>\n") if ($level == 1);
                            push(@toc_lines, "$ind<li>$_");
                        }
                        else
                        {
                            push(@lines1, html_debug("<a name=\"".protect_html($docid)."\"></a>\n",
                                                     __LINE__));
                        }
                        # update DOC
                        push(@lines1, html_debug('', __LINE__));
                        html_reset();
                        $_ =  "<h$level> $name </h$level>\n<!--docid::${docid}::-->\n";
                        $_ = debug($_, __LINE__);
                        push(@lines1, html_debug('', __LINE__));
                    }
                    # update DOC
                    foreach my $line (split(/\n+/, $_))
                    {
                        push(@lines1, "$line\n");
                    }
                    next;
                }
                else
                {
                    warn "$ERROR Bad section line: $_";
                }
            }
            else
            {
                # list item
                if (/^\s*\@itemx?\s+/)
                {
                    my $what = $';
                    $what =~ s/\s+$//;
                    if ($in_bibliography)
                    {
                        if ($what =~ /^$BIBRE$/o)
                        {
                            my $id = 'BIB' . ++$bib_num;
                            $bib2href{$what} = "$docu_doc#$id";
                            print "# found bibliography for '$what' id $id\n"
                                if $T2H_DEBUG & $DEBUG_BIB;
                            $what = t2h_anchor($id, '', $what);
                        }
                    }
                    elsif ($in_glossary && $T2H_USE_GLOSSARY)
                    {
                        my $id = 'GLOSS' . ++$gloss_num;
                        my $entry = $what;
                        $entry =~ tr/A-Z/a-z/ unless $entry =~ /^[A-Z\s]+$/;
                        $gloss2href{$entry} = "$docu_doc#$id";
                        print "# found glossary for '$entry' id $id\n"
                            if $T2H_DEBUG & $DEBUG_GLOSS;
                        $what = t2h_anchor($id, '', $what);
                    }
                    elsif ($in_table && ($table_type eq 'f' || $table_type eq 'v'))
                    {
                        # APA: Insert <dt> before index anchor, if
                        # necessary to produce valid HTML.  Close open
                        # paragraph first.
                        if ($html_element ne 'dt')
                        {
                            # APA: End paragraph, if any.
                            if ($html_element eq 'p')
                            {
                                push(@lines1, debug("</p>\n", __LINE__));
                                html_pop();
                            }
                            push(@lines1, debug("<dt>", __LINE__));
                            html_push('dt');
                        }
                        EnterIndexEntry($table_type, $what, $docu_doc, $section, \@lines1, $in_pre);
                    }
                    # APA: End paragraph, if any.
                    if ($html_element eq 'p')
                    {
                        push(@lines1, debug("</p>\n", __LINE__));
                        html_pop();
                    }
                    if ($html_element =~ m|^d[dlt]$|)
                    {
                        unless ($html_element eq 'dt')
                        {
                            push(@lines1, debug("<dt>", __LINE__));
                        }
                        if ($things_map{$in_table} && !$what)
                        {
                            # special case to allow @table @bullet for instance
                            push(@lines1, debug("$things_map{$in_table}\n", __LINE__));
                        }
                        else
                        {
                            push(@lines1, debug("\@$in_table\{$what\}\n", __LINE__));
                        }
                        push(@lines1, "<dd>");
                        html_push('dd') unless $html_element eq 'dd';
                        #pertusus: FIXME if there is an @itemx there is also 
                        # an empty <dd>
                    }
                    elsif ($html_element eq 'table')
                    {
                        push(@lines1, debug("<tr><td>$what</td>\n", __LINE__));
                        html_push('tr');
                    }
                    elsif ($html_element eq 'tr')
                    {
                        push(@lines1, debug("</tr>\n", __LINE__));
                        push(@lines1, debug("<tr><td>$what</td>\n", __LINE__));
                    }
                    else
                    {
                        push(@lines1, debug("<li>$what\n", __LINE__));
                        html_push('li') unless $html_element eq 'li';
                    }
                    push(@lines1, html_debug('', __LINE__));
                    next;
                }
            }
        }
        # paragraph separator
        if ($_ eq "\n" && ! $in_pre)
        {
            next if $#lines1 >= 0 && $lines1[$#lines1] eq "\n";
            if ($html_element eq 'p')
            {
                push (@lines1, debug("</p>\n<p>\n", __LINE__));
            }
            # 	else
            # 	{
            # 	  push(@lines, "<p></p>\n");
            # 	  $_ = debug("<p></p>\n", __LINE__);
            # 	}
            elsif ($html_element eq 'body' || $html_element eq 'blockquote' || $html_element eq 'dd' || $html_element eq 'li')
            {
                html_push('p');
                push(@lines1, debug("<p>\n", __LINE__));
            }
        }
        # otherwise
        push(@lines1, $_);
        push(@lines1, debug("</div>\n", __LINE__))  if ($tag eq 'center');
    }
    # finish TOC
    my $level = 0;
    while ($level < $curlevel)
    {
        $curlevel--;
        push(@toc_lines, "</li></ul>\n");
    }
    print "# end of pass 1\n" if $T2H_VERBOSE;
}

#+++############################################################################
#                                                                              #
# Stuff related to Index generation                                            #
#                                                                              #
#---############################################################################

sub EnterIndexEntry($$$$$$)
{
    my $prefix = shift;
    my $key = shift;
    my $docu_doc = shift;
    my $section = shift;
    my $lines = shift;
    my $in_pre = shift;

    warn "$ERROR Undefined index command: $_", next
        unless (exists ($index_properties->{$prefix}));
    $key =~ s/\s+$//;
    $key = protect_texi($key);
    my $html_key = substitute_style(protect_html($key));
    $key = unprotect_texi(remove_things(remove_style($key)));

    while (exists $index->{$prefix}->{$key})
    {
        $key .= ' ';
    }

    my $id;
    if ($lines->[$#$lines] =~ /^<!--docid::(.+)::-->$/)
    {
        $id = $1;
    }
    else
    {
        $id = 'IDX' . ++$idx_num;
        push(@$lines, t2h_anchor($id, '', $T2H_INVISIBLE_MARK, !$in_pre));
    }
    $index->{$prefix}->{$key}->{html_key} = $html_key;
    $index->{$prefix}->{$key}->{section} = $section;
    $index->{$prefix}->{$key}->{href} = "$docu_doc#$id";
    print "# found ${prefix}index  for '$key' with id $id\n"
        if $T2H_DEBUG & $DEBUG_INDEX;
}

# returns prefix of @?index command associated with 2 letters prefix name
# for example returns 'c' for 'cp'
sub IndexName2Prefix
{
    my $name = shift;
    my $prefix;

    for $prefix (keys %$index_properties)
    {
        return $prefix if ($index_properties->{$prefix}->{name} eq $name);
    }
    return undef;
}

# get all the entries (for all the prefixes) in the $normal and $code 
# references, formatted with <code> if it is a $code entry.
sub GetIndexEntries($$)
{
    my $normal = shift;
    my $code = shift;
    my $entries = {};
    for my $prefix (keys %$normal)
    {
        for my $key (keys %{$index->{$prefix}})
        {
            $entries->{$key} = {%{$index->{$prefix}->{$key}}};
        }
    }

    if (defined($code))
    {
        for my $prefix (keys %$code)
        {
            unless (exists $normal->{$prefix})
            {
                for my $key (keys %{$index->{$prefix}})
                {
                    $entries->{$key} = {%{$index->{$prefix}->{$key}}};
                    $entries->{$key}->{html_key} = "<code>$entries->{$key}->{html_key}</code>";
                }
            }
        }
    }
    return $entries;
}

# sort according to cmp if both $a and $b are alphabetical or non alphabetical, 
# otherwise the alphabetical is ranked first
sub byAlpha
{
    if ($a =~ /^[A-Za-z]/)
    {
        if ($b =~ /^[A-Za-z]/)
        {
            return lc($a) cmp lc($b);
        }
        else
        {
            return 1;
        }
    }
    elsif ($b =~ /^[A-Za-z]/)
    {
        return -1;
    }
    else
    {
        return lc($a) cmp lc($b);
    }
}

# returns an array of index entries pages splitted by letters
# each page has the following members:
# {First}            first letter on that page
# {Last}             last letter on that page
# {Letters}          ref on an array with all the letters for that page
# {EntriesByLetter}  ref on a hash. Each key is a letter, with value
#                    a ref on arrays of index entries begining with this letter
sub GetIndexPages($)
{
    my $entries = shift;
    my (@Letters);
    my ($EntriesByLetter, $Pages, $page) = ({}, [], {});
    my @keys = sort byAlpha keys %$entries;

    # each index entry is placed according to its first letter in
    # EntriesByLetter
    for my $key (@keys)
    {
        push @{$EntriesByLetter->{uc(substr($key,0, 1))}} , $entries->{$key};
    }
    @Letters = sort byAlpha keys %$EntriesByLetter;
    $T2H_SPLIT_INDEX = 0 unless $T2H_SPLIT;

    unless ($T2H_SPLIT_INDEX)
    {
        $page->{First} = $Letters[0];
        $page->{Last} = $Letters[$#Letters];
        $page->{Letters} = \@Letters;
        $page->{EntriesByLetter} = $EntriesByLetter;
        push @$Pages, $page;
        return $Pages;
    }

    if ($T2H_SPLIT_INDEX =~ /^\d+$/)
    {
        my $i = 0;
        my ($prev_letter);
        for my $letter (@Letters)
        {
            if ($i > $T2H_SPLIT_INDEX)
            {
                $page->{Last} = $prev_letter;
                push @$Pages, $page;
                $i=0;
            }
	    if ($i == 0)
	    {
		$page = {};
		$page->{Letters} = [];
		$page->{EntriesByLetter} = {};
		$page->{First} = $letter;
	    }
            push @{$page->{Letters}}, $letter;
            $page->{EntriesByLetter}->{$letter} = [@{$EntriesByLetter->{$letter}}];
            $i += scalar(@{$EntriesByLetter->{$letter}});
            $prev_letter = $letter;
        }
        $page->{Last} = $Letters[$#Letters];
        push @$Pages, $page;
    }
    return $Pages;
}

sub GetIndexSummary
{
    my $first_page = shift;
    my $Pages = shift;
    my $name = shift;
    my ($summary, $l1, $l2);

    $summary = '<table><tr><th valign="top">Jump to: &nbsp; </th><td>';
    for my $page ($first_page, @$Pages)
    {
        for my $letter (@{$page->{Letters}})
        {
            my $l = t2h_anchor('', "$page->{href}#${name}_$letter",
                            "<b>$letter</b>",
                            0, 'style="text-decoration: none"')
                . "\n &nbsp; \n";
            if ($letter =~ /^[A-Za-z]/)
            {
                $l2 .= $l;
            }
            else
            {
                $l1 .= $l;
            }
        }
    }
    $summary .= $l1 . "<br>\n" if ($l1);
    $summary .= $l2 . '</td></tr></table>';
    return $summary;
}

sub PrintIndexPage
{
    my $lines = shift;
    my $summary = shift;
    my $page = shift;
    my $name = shift;

    push @$lines, $summary;

    push @$lines , <<EOT;
<p></p>
<table border="0">
<tr><td></td><th align="left">Index Entry</th><th align="left"> Section</th></tr>
<tr><td colspan="3"> <hr></td></tr>
EOT

    for my $letter (@{$page->{Letters}})
    {
        push @$lines, '<tr><th>' .
            t2h_anchor("${name}_$letter", '', protect_html($letter)) .
                "</th><td></td><td></td></tr>\n";
        for my $entry (@{$page->{EntriesByLetter}->{$letter}})
        {
            push @$lines,
                "<tr><td></td><td valign=\"top\">" .
                    t2h_anchor('', $entry->{href}, $entry->{html_key}) .
                        "</td><td valign=\"top\">" .
                            t2h_anchor('', sec_href($entry->{section}), clean_name($entry->{section})) .
                                "</td></tr>\n";
        }
        push @$lines, "<tr><td colspan=\"3\"> <hr></td></tr>\n";
    }
    push @$lines, "</table><p></p>";
    push @$lines, $summary;
}

sub PrintIndex
{
    my $lines = shift;
    my $name = shift;
    my $section = shift;
    $section = 'Top' unless $section;
    my $prefix = IndexName2Prefix($name);

    warn ("$ERROR printindex: bad index name: $name"), return
        unless $prefix;

    if ($index_properties->{$prefix}->{code})
    {
        $index_properties->{$prefix}->{from_code}->{$prefix} = 1;
    }
    else
    {
        $index_properties->{$prefix}->{from}->{$prefix}= 1;
    }

    my $Entries = GetIndexEntries($index_properties->{$prefix}->{from},
                                  $index_properties->{$prefix}->{from_code});
    return unless %$Entries;

    if ($T2H_IDX_SUMMARY)
    {
        open(FHIDX, ">$docu_rdir$docu_name" . "_$name.idx")
            || die "Can't open > $docu_rdir$docu_name" . "_$name.idx for writing: $!\n";
        print "# writing $name index summary in $docu_rdir$docu_name" . "_$name.idx...\n" if $T2H_VERBOSE;

        for my $key (sort keys %$Entries)
        {
            print FHIDX "$key\t$Entries->{$key}->{href}\n";
        }
    }

    my $Pages = GetIndexPages($Entries);
    my $page;
    my $first_page = shift @$Pages;
    my $sec_name = $section;
    $sec2index{$section} = 1;

    # remove section number
    $sec_name =~ s/.*? // if $sec_name =~ /^([A-Z]|\d+)\./;

    ($first_page->{href} = sec_href($section)) =~ s/\#.*$//;
    # FIXME $node2sec{$section} might be undef, in that case Sec2PrevNode 
    # returns Top. The resulting html is perfectly right nevertheless.
    # Is it normal that it is undef ? Is Top the right thing ?
    # Why does it work in that case and produce the right output ?
    $node2prev{$section} = Sec2PrevNode($node2sec{$section});
    my $prev_node = $section;
    # Update tree structure of document
    if (@$Pages)
    {
        my $sec;
        my @after;

        while (@sections && $sections[$#sections] ne $section)
        {
            unshift @after, pop @sections;
        }

        for $page (@$Pages)
        {
            my $node = ($page->{First} ne $page->{Last} ?
                        "$sec_name: $page->{First} -- $page->{Last}" :
                        "$sec_name: $page->{First}");
            push @sections, $node;
            $node2sec{$node} = $node;
            $sec2node{$node} = $node;
            $sec2index{$node} = 1;
            $node2up{$node} = $section;
            $page->{href} = next_doc();
            $page->{name} = $node;
            $node2href{$node} = $page->{href};
            if ($prev_node)
            {
                $node2next{$prev_node} = $node;
                $node2prev{$node} = $prev_node;
            }
            $prev_node = $node;
        }
	# Full circle - Next on last index page goes to Top
	$node2next{$prev_node} = "Top";
        push @sections, @after;
    }

    my $summary = GetIndexSummary($first_page, $Pages, $name);
    PrintIndexPage($lines, $summary, $first_page, $name);
    for $page (@$Pages)
    {
        push @$lines, ($T2H_SPLIT eq 'chapter' ? $CHAPTEREND : $SECTIONEND);
        push @$lines, "<h2 align=\"left\">$page->{name}</h2>\n";
        PrintIndexPage($lines, $summary, $page, $name);
    }
}


#+++############################################################################
#                                                                              #
# Pass 2: handle style, menu,  cross-reference, index, footnoter               #
#                                                                              #
#---############################################################################
my @lines2 = ();               # whole document (2nd pass)
my @foot_lines = ();           # footnotes
sub pass2
{
    my $sec;
    my $href;
    my @style_stack=();
    my %state = ();
    $state{'alwayspre'}= 1;
    #my $in_verbatim = 0;        # am I inside a verbatim section
    my $text = '';
    my $in_menu = 0;            # am I inside a menu
    my $in_menu_listing;

    while (@lines1)
    {
        $_ = shift(@lines1);
        #
        # handle @html / @end html and @verbatim / @end verbatim
        #
	#if (/^\@html\s*/)
	#{
	#    push_until ('html', \@lines1, \@lines2);
	#    next;
	#}

	#if ($in_verbatim)
	#{
	#    if(/^\@end verbatim\s*$/)
	#    {
	#        push(@lines2, "</pre>\n");
	#        $in_verbatim = 0;
	#    }
	#    else
	#    {
	#        push(@lines2, protect_html($_));
	#    }
	#    next;
	#}
	#if (/^\@verbatim\s*/)
	#{
	#    $in_verbatim = 1;
	#    push(@lines2, "<pre>\n");
	#    next;
	#}
        #
        # menu
        #

        if (/^\@menu\b/)
        {
            $in_menu = 1;
            $in_menu_listing = 1;
            # APA: <table> implicitly ends paragraph, so let's do it
            # explicitly to keep our HTML stack in sync.
            if ($html_element eq 'p')
            {
                push (@lines2, debug("</p>\n", __LINE__));
                html_pop();
            }
            push(@lines2, debug("<table border=\"0\" cellspacing=\"0\">\n", __LINE__));
            next;
        }
        if (/^\@end\s+menu\b/)
        {
            if ($in_menu_listing)
            {
                push(@lines2, debug("</table>\n", __LINE__));
            }
            $in_menu = 0;
            $in_menu_listing = 0;
            next;
        }
        if ($in_menu)
        {
            my ($node, $name);
            if (s/^\*\s+($NODERE):://o)
            {
                $node = $1;
            }
            elsif (s/^\*\s+(.+):\s+([^\t,\.\n]+)[\t,\.\n]//)
            {
                $name = $1;
                $node = $2;
            }
                #pertusus: makeinfo doesn't complain here
            #elsif (/^\*/)
            #{
                #warn "$ERROR Bad menu line: $_";
            #}
            else
            {
                if ($in_menu_listing)
                {
                    # APA: Handle menu comment lines. These don't end the menu!
                    # $in_menu_listing = 0;
                    # pertusus FIXME for styles split accross lines
                    # pertusus FIXME is the menu considered preformated ?
		    # for spaces ? for'' `` and --- ?
                    push(@lines2, debug('<tr><th colspan="3" align="left" valign="top">' . substitute_text(1, $_) . '</th></tr>
', __LINE__));
                }
            }
            if ($node)
            {
                my $descr = $_;
                if (! $in_menu_listing)
                {
                    $in_menu_listing = 1;
                    push(@lines2, debug("<table border=\"0\" cellspacing=\"0\">\n", __LINE__));
                }
                # look for continuation
                while ($lines1[0] =~ /^\s+[^\s].*$/)
                {
                    $descr .= shift(@lines1);
                }
                menu_entry($node, $name, substitute_text(1,$descr));
            }
            next;
        }
        #
        # printindex
        #
        PrintIndex(\@lines2, $2, $seccount2sec{$1}), next
            if (/^\@printindex_(\d+|)\s+(\w+)/);
        #
        # split style substitutions, footnotes
        #
        scan_line ($_, \$text, \@style_stack, \%state);
        next if (@style_stack);
        $_ = $text;
        $text = '';
        #
        # try to guess bibliography references or glossary terms
        #
        unless (/^<h\d><a name=\"SEC\d/)
        {
            my $done = '';
            while (/$BIBRE/o)
            {
                my ($pre, $what, $post) = ($`, $&, $');
                my $href = $bib2href{$what};
                if (defined($href) && $post !~ /^[^<]*<\/a>/)
                {
                    $done .= $pre . t2h_anchor('', $href, $what);
                }
                else
                {
                    $done .= "$pre$what";
                }
                $_ = $post;
            }
            $_ = $done . $_;

            if ($T2H_USE_GLOSSARY)
            {
                my $done = '';
                while (/\b\w+\b/)
                {
                    my ($pre, $what, $post) = ($`, $&, $');
                    my $entry = $what;
                    $entry =~ tr/A-Z/a-z/ unless $entry =~ /^[A-Z\s]+$/;
                    my $href = $gloss2href{$entry};
                    if (defined($href) && $post !~ /^[^<]*<\/a>/)
                    {
                        $done .= $pre . t2h_anchor('', $href, $what);
                    }
                    else
                    {
                        $done .= "$pre$what";
                    }
                    $_ = $post;
                }
                $_ = $done . $_;
            }
        }
        # otherwise
        push(@lines2, $_);
    }
    print "# end of pass 2\n" if $T2H_VERBOSE;
}

#+++############################################################################
#                                                                              #
# Pass 3: final cleanup                                                        #
#                                                                              #
#---############################################################################

my @doc_lines = ();            # final document

sub pass3
{
    my $end_of_para = 0;        # true if last line is <p>

    # APA: There aint no paragraph before the first one!
    # This fixes a HTML validation error.
    $lines2[0] =~ s|^</p>\n|\n|;
    while (@lines2)
    {
        $_ = shift(@lines2);
        #
        # remove unnecessary <p>
        #
        if (/^\s*<p>\s*$/)
        {
            next if $end_of_para++;
        }
        else
        {
            $end_of_para = 0;
        }
        # otherwise
        push(@doc_lines, $_);
    }

    print "# end of pass 3\n" if $T2H_VERBOSE;
}


#+++############################################################################
#                                                                              #
# Pass 4: print things                                                         #
#                                                                              #
#---############################################################################
sub pass4
{
    $T2H_L2H = l2h_FinishToLatex() if ($T2H_L2H);
    $T2H_L2H = l2h_ToHtml()        if ($T2H_L2H);
    $T2H_L2H = l2h_InitFromHtml()  if ($T2H_L2H);

    my $FH;
    #T2H_InitGlobals();

    # fix node2up, node2prev, node2next, if desired
    if ($has_top_command)
    {
        for my $section (keys %sec2number)
        {
            my $node = $sec2node{$section};
            $node2up{$node} = Sec2UpNode($section) unless $node2up{$node};
            $node2prev{$node} = Sec2PrevNode($section) unless $node2prev{$node};
            $node2next{$node} = Sec2NextNode($section) unless $node2next{$node};
        }
    }

    # prepare %T2H_THISDOC
    $T2H_THISDOC{fulltitle} = $value{'_title'} || $value{'_settitle'} || "Untitled Document";
    $T2H_THISDOC{title} = $value{'_settitle'} || $T2H_THISDOC{fulltitle};
    $T2H_THISDOC{author} = $value{'_author'};
    $T2H_THISDOC{subtitle} = $value{'_subtitle'};
    $T2H_THISDOC{shorttitle} = $value{'_shorttitle'};
    for my $key (keys %T2H_THISDOC)
    {
        #$_ = substitute_style($T2H_THISDOC{$key});
	#$_ = unprotect_texi ($_);
	#s/\s*$//;
	#$T2H_THISDOC{$key} = $_;
	$T2H_THISDOC{$key} =~ s/\s*$//;
    }
    $T2H_THISDOC{program} = $THISPROG;
    $T2H_THISDOC{program_homepage} = $T2H_HOMEPAGE;
    $T2H_THISDOC{program_authors} = $T2H_AUTHORS;
    $T2H_THISDOC{today} = $T2H_TODAY;

    # if no sections, then simply print document as is
    unless (@sections)
    {
        print "# Writing content into $docu_top_file \n" if $T2H_VERBOSE;
        open(FILE, "> $docu_top_file")
            || die "$ERROR: Can't open $docu_top_file for writing: $!\n";

        &$T2H_print_page_head(\*FILE);
        $T2H_THIS_SECTION = \@doc_lines;
        t2h_print_lines(\*FILE);
        &$T2H_print_foot_navigation(\*FILE);
        &$T2H_print_page_foot(\*FILE);
        close(FILE);
        goto Finish;
    }

    # initialize $T2H_HREF, $T2H_NAME
    %T2H_HREF =
        (
         'First',   sec_href($sections[0]),
         'Last',    sec_href($sections[$#sections]),
         'About',   $docu_about . '#SEC_About',
        );

    # prepare TOC, OVERVIEW, TOP
    $T2H_TOC = \@toc_lines;
    $T2H_OVERVIEW = \@stoc_lines;
    if ($has_top)
    {
        my $line_undef = 0;
        my $max_undef = 200;
        while (1)
        {
            $_ = shift @doc_lines;
            unless (defined($_))
            {
                print STDERR "Bug, line undef\n";
                $_ = "<!-- line of doc_lines undef --!>\n";
                $line_undef++;
                if ($line_undef > $max_undef)
                {
                    print STDERR "\n@doc_lines";
                    print STDERR "\n@lines2";
                    die "$max_undef lines undef, infinite recursion ? doc_lines dumped";
                }
            }
            last if /$TOPEND/;
            push @$T2H_TOP, $_;
        }
        $T2H_HREF{'Top'} = $docu_top . '#SEC_Top';
    }
    else
    {
        $T2H_HREF{'Top'} = $T2H_HREF{First};
    }

    $node2href{Top} = $T2H_HREF{Top};
    $T2H_HREF{Contents} = $docu_toc.'#SEC_Contents' if @toc_lines;
    $T2H_HREF{Overview} = $docu_stoc.'#SEC_OVERVIEW' if @stoc_lines;

    # settle on index
    # FIXME pertusus: use first chapter with printindex
    if ($T2H_INDEX_CHAPTER)
    {
        $T2H_HREF{Index} = $node2href{normalise_node($T2H_INDEX_CHAPTER)};
        warn "$ERROR T2H_INDEX_CHAPTER '$T2H_INDEX_CHAPTER' not found\n"
            unless $T2H_HREF{Index};
    }
    if (! $T2H_HREF{Index} && $first_index_chapter)
    {
        $T2H_INDEX_CHAPTER = $first_index_chapter;
        $T2H_HREF{Index} = $node2href{$T2H_INDEX_CHAPTER};
    }

    print "# Using '" . clean_name($T2H_INDEX_CHAPTER) . "' as index page\n"
        if ($T2H_VERBOSE && $T2H_HREF{Index});

    %T2H_NAME =
        (
         'First',   clean_name($sec2node{$sections[0]}),
         'Last',    clean_name($sec2node{$sections[$#sections]}),
         'About',    $T2H_WORDS->{'About_Title'},
         'Contents', $T2H_WORDS->{'ToC_Title'},
         'Overview', $T2H_WORDS->{'Overview_Title'},
         'Index' ,   clean_name($T2H_INDEX_CHAPTER),
         'Top',      clean_name($T2H_TOP_HEADING || $T2H_THISDOC{'title'} || $T2H_THISDOC{'shorttitle'}),
        );

    #############################################################################
    # print frame and frame toc file
    #
    if ( $T2H_FRAMES )
    {
        open(FILE, "> $docu_frame_file")
            || die "$ERROR: Can't open $docu_frame_file for writing: $!\n";
        print "# Creating frame in $docu_frame_file ...\n" if $T2H_VERBOSE;
        &$T2H_print_frame(\*FILE, $docu_toc_frame_file, $docu_top_file);
        close(FILE);

        open(FILE, "> $docu_toc_frame_file")
            || die "$ERROR: Can't open $docu_toc_frame_file for writing: $!\n";
        print "# Creating toc frame in $docu_frame_file ...\n" if $T2H_VERBOSE;
        &$T2H_print_toc_frame(\*FILE, \@stoc_lines);
        close(FILE);
    }


    #############################################################################
    # Monolithic beginning.
    #
    unless ($T2H_SPLIT)
    {
        open(FILE, "> $docu_doc_file")
            || die "$ERROR: Can't open $docu_doc_file for writing: $!\n";
        &$T2H_print_page_head(\*FILE);
    }


    #############################################################################
    # print Top
    #
    if ($has_top)
    {
        if ($T2H_SPLIT)
        {
            open(FILE, "> $docu_top_file")
                || die "$ERROR: Can't open $docu_top_file for writing: $!\n";
        }

        print "# Creating Top in $docu_top_file ...\n" if $T2H_VERBOSE;
        $T2H_THIS_SECTION = $T2H_TOP;
        $T2H_HREF{This} = $T2H_HREF{Top};
        $T2H_NAME{This} = $T2H_NAME{Top};
        &$T2H_print_Top(\*FILE, $has_top_heading);

        if ($T2H_SPLIT)
        {
            close(FILE)
                || die "$ERROR: Error occurred when closing $docu_top_file: $!\n";
        }
    }


    #############################################################################
    # Print sections
    #
    $T2H_NODE{Forward} = $sec2node{$sections[0]};
    $T2H_NAME{Forward} = clean_name($sec2node{$sections[0]});
    $T2H_HREF{Forward} = sec_href($sections[0]);
    $T2H_NODE{This} = 'Top';
    $T2H_NAME{This} = $T2H_NAME{Top};
    $T2H_HREF{This} = $T2H_HREF{Top};
    
    my $previous;
    if ($T2H_SPLIT)
    {
        print "# writing " . scalar(@sections) .
            " sections into $docu_rdir$docu_name"."_[1..$doc_num].$docu_ext"
                if $T2H_VERBOSE;
        $previous = ($T2H_SPLIT eq 'chapter' ? $CHAPTEREND : $SECTIONEND);
        undef $FH;
        $doc_num = 0;
    }
    else
    {
        print "# writing " . scalar(@sections) . " sections in $docu_top_file ..."
            if $T2H_VERBOSE;
        $FH = \*FILE;
        $previous = '';
    }

    my $counter = 0;
    # loop through sections
    my $section;
    while ($section = shift(@sections))
    {
        if ($T2H_SPLIT && ($T2H_SPLIT eq 'section' || $previous eq $CHAPTEREND))
        {
            if ($FH)
            {
                #close previous page
                &$T2H_print_chapter_footer($FH) if $T2H_SPLIT eq 'chapter';
                &$T2H_print_page_foot($FH);
                close($FH);
                undef $FH;
            }
        }
        $T2H_NAME{Back} = $T2H_NAME{This};
        $T2H_HREF{Back} = $T2H_HREF{This};
        $T2H_NODE{Back} = $T2H_NODE{This};
        $T2H_NAME{This} = $T2H_NAME{Forward};
        $T2H_HREF{This} = $T2H_HREF{Forward};
        $T2H_NODE{This} = $T2H_NODE{Forward};
        if ($sections[0])
        {
            $T2H_NODE{Forward} = $sec2node{$sections[0]};
            $T2H_NAME{Forward} = &clean_name($T2H_NODE{Forward});
            $T2H_HREF{Forward} = sec_href($sections[0]);
        }
        else
        {
            delete $T2H_HREF{Forward};
            delete $T2H_NODE{Forward};
            delete $T2H_NAME{Forward};
        }

        my $up_node = $node2up{$T2H_NODE{This}};
        $T2H_HREF{Up} = $node2href{$up_node};
        if (! $T2H_HREF{Up} || $T2H_HREF{Up} eq $T2H_HREF{This})
        {
            $T2H_NAME{Up} = $T2H_NAME{Top};
            $T2H_HREF{Up} = $T2H_HREF{Top};
            $T2H_NODE{Up} = 'Up';
        }
        else
        {
            $T2H_NAME{Up} = clean_name($up_node);
            $T2H_NODE{Up} = $up_node;
        }

        my $prev_node = $node2prev{$T2H_NODE{This}};
        $T2H_NAME{Prev} = clean_name($prev_node);
        $T2H_HREF{Prev} = $node2href{$prev_node};
        $T2H_NODE{Prev} = $prev_node;

	my $fastback_node = Node2FastBack($T2H_NODE{This});
        $T2H_NAME{FastBack} = clean_name($fastback_node);
        $T2H_HREF{FastBack} = $node2href{$fastback_node};
        $T2H_NODE{FastBack} = $fastback_node;

        my $next_node = $node2next{$T2H_NODE{This}};
        $T2H_NAME{Next} = clean_name($next_node);
        $T2H_HREF{Next} = $node2href{$next_node};
        $T2H_NODE{Next} = $next_node;

	my $fastforward_node = Node2FastForward($T2H_NODE{This});
        $T2H_NAME{FastForward} = clean_name($fastforward_node);
        $T2H_HREF{FastForward} = $node2href{$fastforward_node};
        $T2H_NODE{FastForward} = $fastforward_node;

        if (! defined($FH))
        {
            my $file = $T2H_HREF{This};
            $file =~ s/\#.*$//;
            open(FILE, "> $docu_rdir$file") ||
                die "$ERROR: Can't open $docu_rdir$file for writing: $!\n";
            $FH = \*FILE;
            &$T2H_print_page_head($FH);
            t2h_print_label($FH);
            &$T2H_print_chapter_header($FH) if $T2H_SPLIT eq 'chapter';
        }
        else
        {
            t2h_print_label($FH);
        }

        $T2H_THIS_SECTION = [];
        while (@doc_lines)
        {
            $_ = shift(@doc_lines);
            last if ($_ eq $SECTIONEND || $_ eq $CHAPTEREND);
            push(@$T2H_THIS_SECTION, $_);
        }
        $previous = $_;
        &$T2H_print_section($FH);

        if ($T2H_VERBOSE)
        {
            $counter++;
            print "." if $counter =~ /00$/;
        }
    }
    if ($T2H_SPLIT)
    {
        &$T2H_print_chapter_footer($FH) if $T2H_SPLIT eq 'chapter';
        &$T2H_print_page_foot($FH);
        close($FH);
    }
    print "\n" if $T2H_VERBOSE;

    #############################################################################
    # Print ToC, Overview, Footnotes
    #
    delete $T2H_HREF{Prev};
    delete $T2H_HREF{Next};
    delete $T2H_HREF{Back};
    delete $T2H_HREF{Forward};
    delete $T2H_HREF{Up};

    if (@foot_lines)
    {
        print "# writing Footnotes in $docu_foot_file...\n" if $T2H_VERBOSE;
        open (FILE, "> $docu_foot_file") || die "$ERROR: Can't open $docu_foot_file for writing: $!\n"
            if $T2H_SPLIT;
        $T2H_HREF{This} = $docu_foot;
        $T2H_NAME{This} = $T2H_WORDS->{'Footnotes_Title'};
        $T2H_THIS_SECTION = \@foot_lines;
        &$T2H_print_Footnotes(\*FILE);
        close(FILE) if $T2H_SPLIT;
    }

    if (@toc_lines)
    {
        print "# writing Toc in $docu_toc_file...\n" if $T2H_VERBOSE;
        open (FILE, "> $docu_toc_file") || die "$ERROR: Can't open $docu_toc_file for writing: $!\n"
            if $T2H_SPLIT;
        $T2H_HREF{This} = $T2H_HREF{Contents};
        $T2H_NAME{This} = $T2H_NAME{Contents};
        $T2H_THIS_SECTION = \@toc_lines;
        &$T2H_print_Toc(\*FILE);
        close(FILE) if $T2H_SPLIT;
    }

    if (@stoc_lines)
    {
        print "# writing Overview in $docu_stoc_file...\n" if $T2H_VERBOSE;
        open (FILE, "> $docu_stoc_file") || die "$ERROR: Can't open $docu_stoc_file for writing: $!\n"
            if $T2H_SPLIT;
        $T2H_HREF{This} = $T2H_HREF{Overview};
        $T2H_NAME{This} = $T2H_NAME{Overview};
        $T2H_THIS_SECTION = \@stoc_lines;
        my $ul_style = $T2H_NUMBER_SECTIONS ?
            ' style="list-style: none"' : '';
        unshift @$T2H_THIS_SECTION, "<ul${ul_style}>\n";
        push @$T2H_THIS_SECTION, "</ul>\n";
        &$T2H_print_Overview(\*FILE);
        close(FILE) if $T2H_SPLIT;
    }
    my $about_body;
    if ($about_body = &$T2H_about_body())
    {
        print "# writing About in $docu_about_file...\n" if $T2H_VERBOSE;
        open (FILE, "> $docu_about_file") || die "$ERROR: Can't open $docu_about_file for writing: $!\n"
            if $T2H_SPLIT;

        $T2H_HREF{This} = $T2H_HREF{About};
        $T2H_NAME{This} = $T2H_NAME{About};
        $T2H_THIS_SECTION = [$about_body];
        &$T2H_print_About(\*FILE);
        close(FILE) if $T2H_SPLIT;
    }

    unless ($T2H_SPLIT)
    {
        &$T2H_print_page_foot(\*FILE);
        close (FILE);
    }

 Finish:
    l2h_FinishFromHtml() if ($T2H_L2H);
    l2h_Finish() if($T2H_L2H);
    print "# that's all folks\n" if $T2H_VERBOSE;

    exit(0);
}

#+++############################################################################
#                                                                              #
# Low level functions                                                          #
#                                                                              #
#---############################################################################

sub LocateIncludeFile($)
{
    my $file = shift;

    # APA: Don't implicitely search ., to conform with the docs!
    # return $file if (-e $file && -r $file);
    foreach my $dir (@T2H_INCLUDE_DIRS)
    {
        return "$dir/$file" if (-e "$dir/$file" && -r "$dir/$file");
    }
    return undef;
}

sub clean_name($)
{
    my $name = shift;
    $name = remove_style($name);
    $name = unprotect_texi($name);
    return $name;
}

# initialize or increments @appendix_sec_num for the appendix sectionning,
# initialize or increments @normal_sec_num for the normal sections, 
# return the sectionning number.
sub update_sec_num($$)
{
    my($name, $level) = @_;
    my $ret;

    $level--;                   # here we start at 0
    if ($name =~ /^appendix/ || @appendix_sec_num)
    {
        # appendix style
        if (@appendix_sec_num)
        {
            incr_sec_num($level, @appendix_sec_num);
        }
        else
        {
            @appendix_sec_num = ('A', 0, 0, 0);
        }
        $ret = join('.', @appendix_sec_num[0..$level]);
    }
    else
    {
        # normal style
        if (@normal_sec_num)
        {
            incr_sec_num($level, @normal_sec_num);
        }
        else
        {
            @normal_sec_num = (1, 0, 0, 0);
        }
        $ret = join('.', @normal_sec_num[0..$level]);
    }
    $ret .= "." if $level == 0;
    return $ret;
}

sub incr_sec_num
{
    my $level = shift;
    $_[$level]++;
    foreach my $l ($level+1 .. 3)
    {
        $_[$l] = 0;
    }
}

sub Sec2UpNode($)
{
    my $sec = shift;
    my $num = $sec2number{$sec};

    return '' unless $num;
    return 'Top' unless $num =~ /\.\d+/;
    $num =~ s/\.[^\.]*$//;
    $num = $num . '.' unless $num =~ /\./;
    return $sec2node{$number2sec{$num}};
}

# Return previous node or "Top"
sub Sec2PrevNode($)
{
    my $sec = shift;
    my $sec_num = 0;
    if (defined($sec) and defined($sec2seccount{$sec})) 
    { 
        $sec_num = $sec2seccount{$sec} - 1;
    }
    return "Top" if $sec_num < 1;
    return $sec2node{$seccount2sec{$sec_num}};
}

# Return next node or "Top"
sub Sec2NextNode($)
{
    my $sec = shift;
    my $sec_num = $sec2seccount{$sec} + 1;
    return "Top" unless exists $seccount2sec{$sec_num};
    return $sec2node{$seccount2sec{$sec_num}};
}

#
# sub Node2FastBack NODE
#
# INPUTS
#    NODE	A node
#
# RETURNS
#    The beginning of this chapter, or if already there, the beginning of the
#    previous chapter.
#
sub Node2FastBack($)
{
    my $node = shift;
    my $num = $sec2number{$node2sec{$node}};
    my $n;

    # Index Pages  have no section number and 1. should go back to Top
    return $node2prev{$node} if !$num or $num eq "1.";

    # Get the current chapter
    $num =~ /^([A-Z\d]+)\./;
    $n = $1;

    # If the first section of this chapter, decrement chapter
    $n = $n eq 'A' ? $normal_sec_num[0] : $n =~ /^\d+$/ ? --$n : chr(ord($n)-1)
	if $n . '.' eq $num;

    # Return node name for section number "$n."
    return $sec2node{$number2sec{$n . '.'}} || $node2prev{$node};
}

#
# sub Node2FastForward NODE
#
# INPUTS
#    NODE	A node
#
# RETURNS
#    The beginning of the next chapter.
#
sub Node2FastForward($)
{
    my $node = shift;
    my $num = $sec2number{$node2sec{$node}};

    my $n;
    my $returned_node;

    # Index pages
    return $node2next{$node} if !$num;

    # Get current chapter
    $num =~ /^([A-Z\d]+)\./;
    $n = $1;

    # Increment chapter
    $n = $n eq $normal_sec_num[0] ? 'A' : ++$n;

    # Return node name

    if (defined($number2sec{$n . '.'}) and ($returned_node = $sec2node{$number2sec{$n . '.'}}))
    {
        return $returned_node;
    }
    return $node2next{$node};
}

sub open_file($)
{
    my $name = shift;

    ++$fh_name;
    no strict "refs";
    if (open($fh_name, $name))
    {
        unshift(@fhs, $fh_name);
    }
    else
    {
        warn "$ERROR Can't read file $name: $!\n";
    }
    use strict "refs";
}

sub next_line
{
    my($fh, $line);

    if (@input_spool)
    {
        $line = shift(@input_spool);
        return($line);
    }
    while (@fhs)
    {
        $fh = $fhs[0];
        $line = <$fh>;
        return($line) if $line;
        close($fh);
        shift(@fhs);
    }
    return(undef);
}

# shift lines from @$ref_from_lines and push them to @$ref_to_lines until 
# @end $tag is found. If $ref_from_lines is undef, next_line is used.
#
# returns the line with the @end $tag
sub push_until($$$)
{
    my $tag = shift;
    my $ref_from_lines = shift;
    my $ref_to_lines = shift;
    my $line;
    
    while (1)
    {
        if (!defined($ref_from_lines))
        {
             $line = next_line();
        }
        else
        {
             $line = shift @$ref_from_lines;
        }
        last if (!defined($line));
        return $line if ($line =~ /^\@end\s+$tag\s*$/);
        push @$ref_to_lines, $line; 
    }
    die "* Failed to find '$tag' after: " . $ref_to_lines->[$#$ref_to_lines];
}

# used in pass 1, use &next_line
sub skip_until($)
{
    my $tag = shift;
    my $line;
   
    while ($line = next_line())
    {
        return if ($line =~ /^\@end\s+$tag\s*$/);
    }
    die "* Failed to find '$tag' after: " . $lines[$#lines];
}

# used in pass 1 for l2h use &next_line
sub string_until
{
    my $tag = shift;
    my $string = '';
    my $line;

    while ($line = next_line())
    {
        return $string if ($line =~ /^\@end\s+$tag\s.*$/);
        #	$_ =~ s/hbox/mbox/g;
        $string = $string.$line;
    }
    die "* Failed to find '$tag' after: " . $lines[$#lines];
}

sub next_bracketed ($)
{
    my $line = shift;
    my $opened_braces = 0;
    my $result = '';
    while ($line !~ /^\s*$/)
    {
        if ($line =~ s/^([^\{\}]+)//)
        {
            my $text = $1;
            if (!$opened_braces)
            {
                $text =~ s/^\s*//;
                if ($text)
                {
                    $text =~ s/\s*$//;
                    return ($text, $line);
                }
                next;
            }
            $result .= $text;
        }
        if ($line =~ s/^(\{|\})//)
        {
            my $brace = $1;
            $opened_braces++ if ($brace eq '{');
            $opened_braces-- if ($brace eq '}');
	    
            if ($opened_braces < 0)
            {
                warn "$ERROR too much '}' in specification";
                $opened_braces = 0;
                next;
            }
            $result .= $brace;
            return ($result, $line) if ($opened_braces == 0);
        }
    }
    if ($opened_braces)
    {
        warn "$ERROR '{' not closed in specification";
        return ($result . ( '}' x $opened_braces));
    }
}

#
# HTML stacking to have a better HTML output
#

sub html_reset()
{
    @html_stack = ('html');
    $html_element = 'body';
}

sub html_push($)
{
    my $what = shift;
    push(@html_stack, $html_element);
    $html_element = $what;
}

# push the html element on the top the html stack if it is not a <p>
sub html_push_if($)
{
    my $what = shift;
    push(@html_stack, $html_element)
        if ($html_element && $html_element ne 'p');
    $html_element = $what;
}

sub html_pop()
{
    $html_element = pop(@html_stack);
}

sub html_pop_if(;@)
{
    if (@_)
    {
        foreach my $elt (@_)
        {
            if ($elt eq $html_element)
            {
                $html_element = pop(@html_stack) if @html_stack;
                last;
            }
        }
    }
    else
    {
        $html_element = pop(@html_stack) if @html_stack;
    }
}

sub html_debug($$)
{
    my($what, $line) = @_;
    if ($T2H_DEBUG & $DEBUG_HTML)
    {
        $what = "\n" unless $what;
        return("<!-- $line @html_stack, $html_element -->$what")
    }
    return($what);
}

# to debug the output...
sub debug($$)
{
    my($what, $line) = @_;
    return("<!-- $line -->$what")
        if $T2H_DEBUG & $DEBUG_HTML;
    return($what);
}

sub SimpleTexi2Html($)
{
    return undef unless (defined ($_[0]));
    my $text = shift;
    $text = protect_texi($text);
    $text = protect_html($text);
    $text = substitute_style($text);
    return $text;
}

sub normalise_node($)
{
    return undef unless (defined ($_[0]));
    my $text = shift;
    $text = normalise_space ($text);
    $text = SimpleTexi2Html ($text);
    return $text;
}
 
sub normalise_space($)
{
    return undef unless (defined ($_[0]));
    my $text = shift;
    $text =~ s/\s+/ /go;
    $text =~ s/ $//;
    $text =~ s/^ //;
    return $text;
}

sub normalise_space_style($)
{
    return undef unless (defined ($_[0]));
    my $text = shift;
    $text = normalise_space ($text);
    $text = substitute_style($text);
    return $text;
}

sub menu_entry ($$$)
{
    my ($node, $name, $descr) = @_;
    my ($href, $entry);

    $node = normalise_space_style($node);
    $href = $node2href{$node};
    if ($href)
    {
        $descr =~ s/^\s+//;
        $descr =~ s/\s*$//;
        if ($T2H_NUMBER_SECTIONS && !$T2H_NODE_NAME_IN_MENU && $node2sec{$node})
        {
            $entry = $node2sec{$node};
            $name = '';
        }
        else
        {
            $name = normalise_space_style($name);
            $entry = ($name && ($name ne $node || ! $T2H_AVOID_MENU_REDUNDANCY)
                      ? "$name : $node" : $node);
        }

        if ($T2H_AVOID_MENU_REDUNDANCY && $descr)
        {
            my $clean_entry = $entry;
            $clean_entry =~ s/^.*? // if ($clean_entry =~ /^([A-Z]|\d+)\.[\d\.]* /);
            $clean_entry =~ s/[^\w]//g;
            my $clean_descr = $descr;
            $clean_descr =~ s/[^\w]//g;
            $descr = '' if ($clean_entry eq $clean_descr)
        }
        push(@lines2,debug('<tr><td align="left" valign="top">' .
                            t2h_anchor('', $href, $entry, 0, '', 1) .
                            '</td><td>&nbsp;&nbsp;</td>' .
                            '<td align="left" valign="top">' . $descr .
                            "</td></tr>\n", __LINE__));
    }
    elsif ($node =~ /^\(.*\)\w+/)
    {
        push(@lines2,debug('<tr><td align="left" valign="top">' .
                            $entry .
                            '</td><td align="left" valign="top">' . $descr .
                            "</td></tr>\n", __LINE__))
    }
    else
    {
        warn "$ERROR Undefined node of menu_entry ($node): $_";
    }
}

sub do_ctrl($;$) { return "^$_[0]" }

sub do_bullet ($;$)
{ 
    my $text = shift;
    $text = "" unless (defined($text));
    return "$things_map{bullet} $text";
}

sub do_email($;$)
{
    my($addr, $text) = split(/,\s*/, $_[0]);

    $text = $addr unless $text;
    t2h_anchor('', "mailto:$addr", $text);
}

sub do_sc($;$)
{
    # l2h does this much better
    return l2h_ToLatex("{\\sc ".unprotect_html($_[0])."}") if ($T2H_L2H);
    return "\U$_[0]\E";
}

sub do_math($;$)
{
    # APA: FIXME
    # This sub doesn't seem to be used.
    return l2h_ToLatex("\$".unprotect_html($_[0])."\$") if ($T2H_L2H);
    return '<em>' . $_[0] . '</em>';
}

sub do_uref($;$)
{
    my($url, $text, $only_text) = split(/,\s*/, $_[0]);
    # APA: Don't markup obviously bad links.
    # e.g. texinfo.texi 4.0 has this, which would lead to a broken
    # link:
    # @section @code{@@uref@{@var{url}[, @var{text}][, @var{replacement}]@}}
    #return if $url =~ /[<>]/;
    $text = $only_text if $only_text;
    $text = $url unless $text;
    t2h_anchor('', $url, $text);
}

sub do_url($;$) { return t2h_anchor('', $_[0], $_[0]); }

sub do_acronym($;$)
{
    return '<font size="-1">' . $_[0] . '</font>';
}

sub do_accent($$)
{
    return "$_[0]''" if $_[1] eq 'H';
    return "$_[0]." if $_[1] eq 'dotaccent';
    return "$_[0]*" if $_[1] eq 'ringaccent';
    return "$_[0]".'[' if $_[1] eq 'tieaccent';
    return "$_[0]".'(' if $_[1] eq 'u';
    return "$_[0]_" if $_[1] eq 'ubaraccent';
    return ".$_[0]" if $_[1] eq 'udotaccent';
    return "$_[0]&lt;" if $_[1] eq 'v';
    return "&$_[0]cedil;" if $_[1] eq ',';
    return "$_[0]" if $_[1] eq 'dotless';
    return "&$_[0]$accent_map{$_[1]};" if $accent_map{$_[1]};
    return undef;
}

sub end_paragraph($)
{
   my $text = shift;
   print STDERR "Empty paragraph\n" if ($text =~ /^\s*$/);
   return '' if ($text =~ /^\s*$/);
   return "<p>$text\n</p>";
}

sub do_text_macro($$$)
{
    my $type = shift;
    my $line = shift;
    my $state = shift;

    if (!grep {$type eq $_} @kept_text)
    { # ignored text
        $state->{'ignored'} = $type;
    }
    elsif (grep {$type eq $_} @raw_text)
    {
        $state->{'raw'} = $type;
    }
    elsif (grep {$type eq $_} @value_text)
    {
        if ($line =~ s/\s+($VARRE)$//o or $line =~ s/\s+($VARRE)\s//o)
        {
            if ($type eq 'ifclear')
            {
                if (defined($value{$1}))
                {
                    $state->{'ignored'} = $type;
                }
                else
                {
                    push @{$state->{'text_macro_stack'}}, $type;
                }
            }
            elsif ($type eq 'ifset')
            {
                unless (defined($value{$1}))
                {
                    $state->{'ignored'} = $type;
                }
                else
                {
                    push @{$state->{'text_macro_stack'}}, $type;
                }
            }
        }
        else
        {
            warn "$ERROR Bad $type line: $line";
        }
    }
    else
    {
        $state->{'titlepage'} = 1 if ($type eq 'titlepage');
        push @{$state->{'text_macro_stack'}}, $type;
    }
    return $line;
}

sub do_raw
{
    my $style = shift;
    my $text = shift;
    if ($style eq 'verbatim')
    {
        return '<pre>' . protect_htm($text) . '</pre>';
    }
    elsif ($style eq 'html')
    {
        return $text;
    }
    elsif ($style eq 'tex')
    {
        # add to latex2html file
        if ($T2H_L2H)
        {
            # add space to the end -- tex(i2dvi) does this, as well
            return (l2h_ToLatex($text . " "));
        }
        else
        {
            return '';
        }
    }
    else
    {
        warn "$WARN (bug) unknown style $style\n";
        return protect_htm($text);
    }
}

# process definition commands
sub begin_deff
{
    my $tag = shift;
    my $line = shift;
    my $state = shift;
    
    if (defined($def_map{$tag}) and $def_map{$tag})
    {
        # substitute shortcuts for definition commands
        my $substituted = $def_map{$tag};
        $substituted =~ s/(\w+)//;
        $tag = $1;
        $line = $substituted . $line;
    }
    
    my $result = '';
    my $is_extra = 0;
    if ($tag =~ s/x$//)
    {
      # extra definition line
        $is_extra = 1;
    }
    my $type;
    ($type, $line) = next_bracketed($line);
    $type =~ s/^\{(.*)\}$/$1/;
    $type = substitute_text(1, $type);
    $type .= ':' if (!$T2H_DEF_TABLE); # it's nicer like this
    my $name;
    ($name, $line) = next_bracketed($line);
    $name =~ s/^\{(.*)\}$/$1/;
    $name = substitute_text(1, $name);
    print "# def ($tag): {$type} {$name}" . "$line"
        if $T2H_DEBUG & $DEBUG_DEF;
    my $ftype;
    if ($is_extra)
    {
        $result = debug($T2H_DEF_TABLE ? '' : '<dt>', __LINE__);
        #$_ = &debug("<tr TEST1>\n", __LINE__) if ($T2H_DEF_TABLE);
    }
    else
    {
         # APA: <dl> implicitly ends paragraph, so let's
         # do it explicitly to keep our HTML stack in sync.
         if ($html_element eq 'p')
         {
             $result = debug("</p>\n", __LINE__);
             html_pop();
         }
         else
         {
             $result = '';
         }
         $result .= debug($T2H_DEF_TABLE ?
         "<table width=\"100%\">\n" : "<dl>\n<dt>", __LINE__);
    }
    if ($tag eq 'deffn' || $tag eq 'defvr' || $tag eq 'deftp')
    {
        if ($T2H_DEF_TABLE)
        {
            $result .= "<tr>\n<td align=\"left\"><b>$name</b>\n";
            $result .= substitute_text(1, $line);
            $result .= "</td>\n";
            $result .= "<td align=\"right\">";
            $result .= "$type</td>\n</tr>\n";
        }
        else
        {
            $result .= "<u>$type</u> <b>$name</b>";
            $result .= substitute_text(1, $line);
        }
    }
    elsif ($tag eq 'deftypefn' || $tag eq 'deftypevr'
        || $tag eq 'deftypeop' || $tag eq 'defcv'
        || $tag eq 'defop')
    {
        $ftype = $name;
        ($name, $line) = next_bracketed($line);
        $name =~ s/^\{(.*)\}$/$1/;
        $name = substitute_text(1, $name);
        if ($T2H_DEF_TABLE)
        {
            $result .= "<tr>\n<td align=\"left\"><b>$name</b>";
            $result .= substitute_text(1, $line);
            $result .= "</td>\n";
            $result .= "<td align=\"right\">";
            $result .= "$type of $ftype</td>\n</tr>\n";
        }
        else
        {
            $result .= "<u>$type</u> $ftype <b>$name</b>";
            $result .= substitute_text(1, $line);
        }
    }
    else
    {
        warn "$ERROR Unknown definition type: $tag\n";
        $result .= "<u>$type</u> <b>$name</b>";
        $result .= substitute_text(1, $line);
    }
    $result .= debug("\n<dd>", __LINE__) if (!$T2H_DEF_TABLE);
                ########$_ .= &debug("\n</table TEST3>\n<table width=\"95%\">\n", __LINE__) if ($T2H_DEF_TABLE);
    $name = unprotect_html($name);
    if ($tag eq 'deffn' || $tag eq 'deftypefn')
    {
        #EnterIndexEntry('f', $name, $docu_doc, $section, \@lines1, $in_pre);
    }
    elsif ($tag eq 'defop')
    {
    #    EnterIndexEntry('f', "$name on $ftype", $docu_doc, $section, \@lines1, $in_pre);
    }
    elsif ($tag eq 'defvr' || $tag eq 'deftypevr' || $tag eq 'defcv')
    {
         #EnterIndexEntry('v', $name, $docu_doc, $section, \@lines1, $in_pre);
                   #		unshift(@input_spool, "\@vindex $name\n");
    }
    else
    {
         #EnterIndexEntry('t', $name, $docu_doc, $section, \@lines1, $in_pre);
                    #		unshift(@input_spool, "\@tindex $name\n");
    }
    return $result;
}

sub begin_table
{
    my $type = shift;
    my $line = shift;
    my $state = shift;
}

#
# xref
#
sub do_ref
{
    my $macro = shift;
    my $text = shift;
    my $type = $macro;    

    my $result = '';
    # note: Texinfo may accept other characters
    if ($type eq 'xref')
    {
        $type = "$T2H_WORDS->{'See'} ";
    }
    elsif ($type eq 'pxref')
    {
        $type = "$T2H_WORDS->{'see'} ";
    }
    elsif ($type eq 'inforef')
    {
         $type = "$T2H_WORDS->{'See'} Info";
    }
    else
    {
         $type = '';
    }
    #chop($text);   # remove final newline
    $text =~ s/\s+/ /gos; # remove useless spaces and newlines
    my @args = split(/\s*,\s*/, $text);
    my $node = $args[0];   # the node is always the first arg
    $node = normalise_space($node);
    if ($node =~ s/^\(([^\s\(\)]+)\)\s*//)
    {
         $args[3] = $1 unless ($args[3]);
         $args[0] = $node;
    }
    my $sec = $args[2] || $args[1] || $node2sec{$node};
    my $href = $node2href{$node};
    if (@args == 5)
    {                   # reference to another manual
         $sec = $args[2] || $node;
         my $man = $args[4] || $args[3];
         $result = "${type}$T2H_WORDS->{'section'} `$sec' in " . apply_style ('cite', $man);
    }
    elsif ($type =~ /Info/)
    {                   # inforef 
        warn "$ERROR Wrong number of arguments: \@$macro" ."$text" unless @args == 3;
        my $in_file = $args[2];
        $result = "${type} file `$in_file', node `$node'";
    }
    elsif (@args == 4)
    {        # ref to info file        
        my $in_file = $args[3];
        if ($node)
        {
             $result = "${type}Info `$in_file', node `$node'";
        }
        else 
        {
             $result = "${type}${in_file}";
        }
    }
    elsif ($sec && $href && ! $T2H_SHORT_REF)
    {
        $result  = "$type";
        $result .= "$T2H_WORDS->{'section'} " if $type;
        $result .= t2h_anchor('', $href, $sec, 0, '', 1);
    }
    elsif ($href)
    {
        $result = "${type} " .
            t2h_anchor('', $href, $args[2] || $args[1] || $node, 0, '', 1);
    }
    else
    {
        warn "$ERROR Undefined node ($node) in $macro: $text";
        $result = "\@$macro"."{${text}}";
    }
    return $result;
}

sub do_footnote($$)
{
    my $command = shift;
    my $text = shift;

    if ($command =~ /^footnote_(\d+)$/)
    {
        my $docu = doc_href($1);
        $foot_num++;
        my $docid  = "DOCF$foot_num";
        my $footid = "FOOT$foot_num";
        my $foot = "($foot_num)";
        push(@foot_lines, "<h3>" . t2h_anchor($footid, "$docu#$docid", $foot) . "</h3>\n");
        $text = "<p>$text" unless $text =~ /^\s*<p>/;
        push(@foot_lines, "$text\n");
        return t2h_anchor($docid, "$docu_foot#$footid", $foot);
    }
    else
    {
        die "Bug, bad footnote command $command";
    }
}

sub do_image
{
    # replace images
    my $macro = shift;
    my $text = shift;
    $text =~ s/\s+/ /gos; # remove useless spaces and newlines
    my @args = split (/\s*,\s*/, $text);
    my $base = $args[0];
    my $image =
         LocateIncludeFile("$base.$args[4]") if ($args[4]) ||
         LocateIncludeFile("$base.png") ||
         LocateIncludeFile("$base.jpg") ||
         LocateIncludeFile("$base.gif");
    warn "$ERROR no image file for $base: $text" unless ($image && -e $image);
    return ($T2H_CENTER_IMAGE ?
          "<div align=\"center\"><img src=\"$image\" alt=\"$base\"></div>" :
          "<img src=\"$image\" alt=\"$base\">");
}

sub apply_style($$;$)
{
    my($texi_style, $text, $close_or_no_close) = @_;
    my($style);
    
    my $no_close;
    my $no_open;
    
    if (defined($close_or_no_close))
    {
        $no_close = 1 if (($close_or_no_close) eq 'no_close');
        $no_open = 1 if (($close_or_no_close) eq 'no_open');
    }
    
    $style = $style_map{$texi_style};
    if (defined($style))
    {                           # known style
        my $do_quotes = 0;
        if ($style =~ s/^\"//)
        {                       # add quotes
            $do_quotes = 1;
        }
        if ($style =~ s/^\&//)
        {                       # custom
            no strict "refs";
            $text = &$style($text, $texi_style);
            use strict "refs";
        }
        elsif ($style)
        {                       # good style
            $text = "<$style>$text</$style>";
        }
        else
        {                       # no style
        }
        if ($do_quotes)
        {
            if (!$no_close and !$no_open)
            {
                $text = "\`$text\'";
            }
            elsif ($no_close)
            {
                $text = "\`$text";
            }
            elsif ($no_open)
            {
                $text = "$text\'";
            }
        }
    }
    else
    {                           # unknown style
        $text = undef;
    }
    return($text);
}

# remove Texinfo styles
sub remove_style($)
{
    my $text = shift;
    1 while($text =~ s/\@\w+{([^\{\}]+)}/$1/g);
    return $text;
}

sub remove_things($)
{
    my $text = shift;
    $text =~ s|\@(\w+)\{\}|$1|g;
    return $text;
}

sub substitute_style($)
{
    my $line = shift;
    my($changed, $done, $style, $text);

    $line = simple_substitutions($line);
    $changed = 1;
    while ($changed)
    {
        $changed = 0;
        $done = '';
        while (($line =~ /\@(\w+){([^\{\}]*)}/) || ($line =~ /\@(,){([^\{\}]+)}/))
        {
            $2 = '' if (!defined($2));
            $text = apply_style($1, $2);
            if (defined($text))
            {
                $line = "$`$text$'";
                $changed = 1;
            }
            else
            {
                $done .= "$`\@$1";
                $line = "{$2}$'";
            }
        }
        $line = $done . $line;
    }
    return $line;
}

sub scan_line0($$)
{
    my $line = shift;
    my $state = shift;
    my $result = undef;

    local $_ = $line;
    
    while(1)
    {
        if (defined($state->{'ignored'}))
        {
            if (s/^.*?\@end\s+$state->{'ignored'}\s+// or s/^.*?\@end\s+$state->{'ignored'}$//)
            {
                 $state->{'ignored'} = undef;
                 next if (defined($result) or $_);
            }
            return $result;
        }

        if (defined($state->{'raw'}))
        {
            if (s/^(.*?\@end\s$state->{'raw'}\s)\s*// or s/^(.*?\@end\s$state->{'raw'})$//)
            {
                 add_prev(\$result, $1);
                 $state->{'raw'} = undef;
                 next;
            }
	    else
            {
                 add_prev(\$result, $_);
                 return $result;
            }
        }
       
        if (defined($state->{'verb'}))
        {
            my $char = quotemeta($state->{'verb'});
            if (s/^(.*?)$char\}//)
            {
                 add_prev(\$result, $1 . $state->{'verb'} . '}');
                 $state->{'verb'} = undef;
                 next;
            }
            else
            {
                 add_prev(\$result, $_);
                 return $result;
            }
        }
	if (s/^([^@]*)\@end\s+([a-zA-Z]\w*)\s*$// or s/^([^@]*)\@end\s+([a-zA-Z]\w*)(\s)//)
        {
            add_prev(\$result, $1) if ($1);
            my $end_tag = $2;
            my $spaces = $3;
            if (defined($state->{'text_macro_stack'}) 
               and @{$state->{'text_macro_stack'}} 
               and ($end_tag eq $state->{'text_macro_stack'}->[-1]))
            {
                 pop @{$state->{'text_macro_stack'}};
            }
            elsif (grep {$_ eq $end_tag} @kept_text)
            {
                 warn "$ERROR \@end $end_tag without corresponding element\n";
            }
	    else
            {
                $state->{'complex_format'}-- if ($state->{'complex_format'} and $complex_format_map->{$end_tag});
                #another end tag
                add_prev(\$result, "\@end $end_tag" . (defined($spaces) ? $spaces : ''));
                next;
            }

            $state->{'titlepage'} = 0 if ($end_tag eq 'titlepage');
            next if (defined($result) or $_);
            return $result;            
        }
	elsif (s/^([^@]*)\@([a-zA-Z]\w*|["'@}{,.!?\s*-^`=])//o)
        {
            add_prev(\$result, $1) if ($1);
            my $macro = $2;
            if ($macro =~ /^(c|comment)$/)
            {
                return $result unless (defined($result));
                s/.*//;
                next;
            }
            if (grep {$macro eq $_} @text_macros)
            {
                $_ = do_text_macro ($macro, $_, $state);
                #if it is a raw formatting command we must keep it for later
                if (defined($state->{'raw'}))
		{
                    add_prev(\$result, "\@$macro") ;
                    next;
		}
                return $result if (!defined($result) and /^\s*$/);
                next;
            }
            if ($macro eq 'verb' and s/^{//)
            {
                if (/^$/)
                {
                    warn "$ERROR verb at end of line";
                    add_prev(\$result, '@verb{');
                }
                else
                {
                    s/^(.)//;
                    $state->{'verb'} = $1;
                    add_prev(\$result, '@verb{' . $1);
                }
                next;
            }
            $state->{'complex_format'}++ if ($complex_format_map->{$macro});
            add_prev(\$result, "\@$macro");
            next;
        }
        add_prev(\$result, $_);
        return $result;
    }
}

sub scan_line($$$$)
{
    my $line = shift;
    my $text = shift;
    my $stack = shift;
    my $state = shift;

    die "stack not an array ref"  unless (ref($stack) eq "ARRAY");
    local $_ = $line;
    
    unless ($state->{'raw'} or $state->{'complex_format'}  or $state->{'alwayspre'} or $state->{'no_paragraph'})
    {
        if (/^$/)
        {
            print STDERR "Empty line\n";
            if ($state->{'paragraph'})
            {
                my $new_stack = [];
                print STDERR "Close paragraph\n";
                ($text, $stack, $state, $new_stack) = close_stack($text, $stack, $state, 1);
                my $paragraph = pop @$stack;
                if (!$paragraph->{'format'} or 
                    (!($paragraph->{'format'} eq 'paragraph')))
                {
                    print STDERR "Bug: paragraph closed but no paragraph\n";
                }
                add_prev ($text, $stack, end_paragraph($paragraph->{'text'}));
                $state->{'paragraph_nr'}--;
                $state->{'paragraph'} = 0;
                $state->{'paragraph_macros'} = $new_stack;
                return;
            }
        }
	elsif (!$state->{'paragraph'})
        {
            print STDERR "New paragraph. Line $_";
            $state->{'paragraph'} = 1;
            $state->{'paragraph_nr'}++;
            push @$stack, {'format' => 'paragraph', 'text' => '' };
            push @$stack, @{$state->{'paragraph_macros'}} if $state->{'paragraph_macros'};
            $state->{'paragraph_macros'} = undef;
        }
    }
    
    while(1)
    {
        if (defined($state->{'raw'})) 
        {
            die "Bug" if (! @$stack or ! ($stack->[-1]->{'style'} eq $state->{'raw'}));
            if (s/^(.*?)\@end\s$state->{'raw'}\s// or s/^(.*?)\@end\s$state->{'raw'}$//)
            {
                add_prev ($text, $stack, $1);
                my $style = pop @$stack;
                if ($style->{'text'} !~ /^\s*$/) 
                {
                   add_prev($text, $stack, do_raw($style->{'style'}, $style->{'text'}));
                }
                $state->{'raw'} = undef;
                next;
            }
	    else
            {
                 add_prev ($text, $stack, $_);
                 last;
            }
        }
	
        if (defined($state->{'verb'}))
        {
            my $char = quotemeta($state->{'verb'});
            if (s/^(.*?)$char\}/\}/)
            {
                 add_prev($text, $stack, protect_htm($1));
                 $state->{'verb'} = undef;
                 next;
            }
            else
            {
                 add_prev($text, $stack, protect_htm($_));
                 last;
            }
        }
	
	if (s/^([^{}@]*)\@end\s+([a-zA-Z]\w*)\s// or s/([^{}@]*)^\@end\s+([a-zA-Z]\w*)$//)
        {
            add_prev($text, $stack, protect_htm($1, $state));
            my $end_tag = $2;
                 
            if (!@$stack)
            {
                warn "$ERROR \@end $end_tag without corresponding opening element\n";
                add_prev($text, $stack, "\@end $end_tag");
                next;
            }
            my $format = pop @$stack;
	    {
                #FIXME what to do if $style->{'style'} != $end_tag; do_table, format....
                if (!$format->{'format'})
                {
                    warn "$ERROR waiting for end of $format->{'style'}, found $end_tag";
                    push @$stack, $format;
                    add_prev($text, $stack, "\@end $end_tag");
                }
		else
                {
                    if (!($format->{'format'} eq $end_tag))
                    {
                        warn "$ERROR waiting for end of $format->{'format'}, found $end_tag";
                    }
                    if ($format->{'format'} eq 'paragraph')
                    {
                        add_prev($text, $stack, end_paragraph($format->{'text'}));
                    }
                    if (defined($def_map{$end_tag}))
                    {
                        add_prev($text, $stack, $format->{'text'});
                    }
                    else 
                    {
                        warn "$WARN Unknown \@end $end_tag\n";
                        add_prev($text, $stack, "\@end $end_tag");
                    }
                }
                next;
            }
        }
	elsif (s/^([^{}@]*)\@([a-zA-Z]\w*|["'@}{,.!?\s*-^`=])//)
        {
            add_prev($text, $stack, protect_htm($1, $state));
            my $macro = $2;
            if ($simple_map{$macro})
            {
                add_prev($text, $stack, $simple_map{$macro});
                next;
            }
            if (grep {$macro eq $_} @raw_text)
            {
                $state->{'raw'} = $macro;
                push (@$stack, {'style' => $macro, 'text' => ''});
                next;
            }
            if (s/^{//)
            {
                if ($macro eq 'verb')
                {
                    if (/^$/)
                    {
                        warn "$ERROR verb at end of line";
                    }
                    else
                    {
                        s/^(.)//;
                        $state->{'verb'} = $1;
                    }
                }
                push (@$stack, { 'style' => $macro, 'text' => '' });
                next;
            }
            if ($accent_map{$macro})
            {
                if (s/^([^\s])//)
                {
                    add_prev ($text, $stack, do_accent ($macro, $1));
                }
                else
                {
                    add_prev ($text, $stack, do_accent ($macro, ''));
                }
                next;
            }
            if ($things_map{$macro})
            {
                warn "$ERROR $macro requires {}\n";
                add_prev ($text, $stack, $things_map{$macro});
                next;
            }
            # FIXME begin table, def
            if (defined($def_map{$macro}))
            {
                push @$stack, { 'format' => $macro, 'text' => begin_deff($macro, $_, $state) };
                $_ = "";
                next;
            }
            warn "$WARN Unknown macro $macro (left as is)\n";
            add_prev ($text, $stack, protect_htm("\@$macro"));
            next;
        }
	elsif(s/^([^{}@]*)\@(.)//)
	{
            warn "$ERROR Unkown command: `$2', line: $line";
            add_prev($text, $stack, protect_htm("\@$2"));
            next;
        }
        elsif (s/^([^{}]*)([{}])//)
        {
            add_prev($text, $stack, protect_htm($1, $state));
            if ($2 eq '{')
            {
                add_prev($text, $stack, '{<!-- brace without macro -->');
                warn "$ERROR '{' without macro line: $line";
            }
            else
            {
                if (@$stack)
                {
                    my $style = pop @$stack;
                    my $result;
                    if ($style->{'style'})
                    {
                        $style->{'no_close_open'} = 'no_close' if ($state->{'no_close'});
                        if ($style->{'style'} =~ /^footnote_\d+$/)
                        {
                            $result = do_footnote($style->{'style'}, $style->{'text'});
                        }
                        elsif ($style->{'style'} =~ /^(x|px|info|)ref$/)
                        {
                            $result = do_ref($style->{'style'}, $style->{'text'});
                        }
                        elsif ($style->{'style'} eq 'image')
                        {
                            $result = do_image($style->{'style'}, $style->{'text'});
                        }
                        elsif ($things_map{$style->{'style'}})
                        {
                            $result = $things_map{$style->{'style'}} . $style->{'text'};
                        }
                        else
                        {
                             $result = apply_style($style->{'style'}, $style->{'text'}, $style->{'no_close_open'});
                        }
                        if (!defined($result))
                        {
                            $result = '@' . $style->{'style'} . '{' . $style->{'text'} . '}';
                        }
                    }
                    else
                    {
                        $result = $style->{'text'} . '}';
                    }
                    add_prev ($text, $stack, $result);
                    next;
                }
                else
                {
                    warn "$ERROR '}' without opening '{' line: $line";
                }
            }
	}
	else
        {
            add_prev($text, $stack, protect_htm($_, $state));
            last;
        }
    }
    return undef;
}

sub protect_htm($;$)
{
    my $text = shift;
    my $state = shift;
    if (defined($state) and !$state->{'complex_format'} and !$state->{'alwayspre'})
    {
        $text =~ s/``/"/go;
        $text =~ s/''/"/go;
        $text =~ s/---/--/go;
    }
    return $text;
}

sub add_prev ($$;$)
{
    my $text = shift;
    my $stack = shift;
    my $string = shift;

    if (!defined($stack) or (!(ref($stack) eq "ARRAY")))
    {
        $string = $stack;
        $stack = [];
    }
    return if (!defined($string));
    if (@$stack)
    {
        $stack->[-1]->{'text'} .= $string;
        return;
    }

    if (!defined($$text))
    {
         $$text = $string;
    }
    else
    {
         $$text .= $string;
    }
}

# We don't close external formats or footnotes if close_paragraph is true
# 
sub close_stack($$$;$)
{
    my $text = shift;
    my $stack = shift;
    my $state = shift;
    my $close_paragraph = shift;
    my $new_stack = [];
    
    return ($text, $state, $stack, $new_stack) unless @$stack;
    
    my $stack_level = $#$stack + 1;
    my $string = '';
    my $verb = '';
    
    if ($state->{'verb'})
    {
        $string .= $state->{'verb'};
        $verb = $state->{'verb'};
    }
    if ($state->{'raw'})
    {
        if ($close_paragraph)
        {
            print STDERR "Bug: close_paragraph is true and we're in raw";
        }
        $string .= " \@end $state->{'raw'} ";
        $stack_level--;
    }
    
    while ($stack_level--)
    {
        last if ($close_paragraph and $stack->[$stack_level]->{'format'} 
             and ($stack->[$stack_level]->{'format'} eq 'paragraph'));
        if ($stack->[$stack_level]->{'format'})
        {
            $string .= " \@end $stack->[$stack_level]->{'format'} ";
        }
        else
        {
            if ($close_paragraph)
            { #duplicate the stack
                push @$new_stack, { 'style' => $stack->[$stack_level]->{'style'},
                    'text' => '', 'no_close_open' => 'no_open' };
            }
            $string .= '} ';
        }
    }
    $state->{'no_close'} = $close_paragraph;
    scan_line ($string, $text, $stack, $state);
    $state->{'no_close'} = 0;
    $state->{'verb'} = $verb if ($verb);
    return ($text, $stack, $state, $new_stack);
}

sub substitute_text ($@)
{
    my $no_paragraph = shift;
    my @stack = ();
    my %state = ();
    my $text = '';
    my $result = '';
    if ($no_paragraph)
    {
        $state{'no_paragraph'} = 1;
    }
    $state{'paragraph'} = 0;
    
    while (@_)
    {
        my $line = shift @_;
	next unless (defined($line));
	scan_line ($line, \$text, \@stack, \%state);
        next if (@stack);
        $result .= $text;
        $text = '';
    }
    close_stack (\$text, \@stack, \%state);
    return $result . $text;
}

sub t2h_anchor($$$;$$$)
{
    my($name, $href, $text, $newline, $extra_attribs, $dont_html) = @_;
    my @result = ();

    push(@result, '<a');
    if ($name)
    {
        if ($dont_html)
        {
            push(@result, ' name="', $name, '"') if $name;
        }
        else
        {
            push(@result, ' name="', protect_html($name), '"') if $name;
        }
    }
    if ($href)
    {
        $href =~ s|^$T2H_HREF_DIR_INSTEAD_FILE|./|
            if ($T2H_HREF_DIR_INSTEAD_FILE);
        if ($dont_html)
        {
            push(@result, ' href="', $href, '"');
        }
        else
        {
            push(@result, ' href="', protect_html($href), '"');
        }
    }
    push(@result, ' ', $extra_attribs) if $extra_attribs;
    push(@result, '>');
    push(@result, $text) if $text;
    push(@result, '</a>');
    push(@result, "\n") if $newline;

    return join('', @result);
}

sub doc_href($)
{
    my $num = shift;

    return "$docu_name.$docu_ext" if ($num == 0);
    return("${docu_name}_$num.$docu_ext");
}

sub sec_href
{
    return $node2href{$sec2node{$_[0]}};
}

sub next_doc()
{
    ++$doc_num;
    if ("$docu_rdir${docu_name}_$doc_num.$docu_ext" eq "$docu_top_file") {
        warn "$WARN Section $docu_rdir${docu_name}_$doc_num.$docu_ext would overwrite Top, continuing at $docu_rdir${docu_name}_".($doc_num+1).".$docu_ext";
        $doc_num++;
    }
    $docu_doc = doc_href($doc_num);
}

sub t2h_print_lines($;$)
{
    my ($fh, $lines) = @_;

    $lines = $T2H_THIS_SECTION unless $lines;
    my $cnt = 0;
    my @cnt;
    for my $line (@$lines)
    {
        $line = l2h_FromHtml($line) if ($T2H_L2H);
        $line =  unprotect_texi ($line);
        print $fh $line;
        @cnt = split(/\W*\s+\W*/);
        $cnt += scalar(@cnt);
    }
    return $cnt;
}

# protect the , in @,{}
sub protect_cedilla($)
{
    my $text = shift;
    $text =~ s/\@,\{(\w)\}/$;6$1/g;
    return $text;
}

sub protect_texi($)
{
    # protect @ { } ` '
    my $text = shift;
    $text =~ s/\@\@/$;0/go;
    $text =~ s/\@\{/$;1/go;
    $text =~ s/\@\}/$;2/go;
    $text =~ s/\@\`/$;3/go;
    $text =~ s/\@\'/$;4/go;
    $text =~ s/\@\"/$;5/go;
    return $text;
}

sub protect_html($)
{
    my $what = shift;
    # protect &, ", <, and >.
    # APA: Keep it simple.  This is what perl's CGI::espaceHTML does.
    # We may consider using that instead.
    # If raw HTML is used outside @ifhtml or @html it's an error
    # anyway.
    $what =~ s/\&/\&amp;/go;
    $what =~ s/\"/\&quot;/go;
    $what =~ s/\</\&lt;/go;
    $what =~ s/\>/\&gt;/go;
    return($what);
}

sub unprotect_cedilla(@)
{
    my @result = ();
    while (@_)
    {
        my $text = shift;
        $text =~ s/$;6(\w)/\@,\{$1\}/g;
        push @result, $text;
    }
    return @result;
}

sub unprotect_texi($)
{
    my $text = shift;
    $text =~ s/$;0/\@/go;
    $text =~ s/$;1/\{/go;
    $text =~ s/$;2/\}/go;
    $text =~ s/$;3/\`/go;
    $text =~ s/$;4/\'/go;
    $text =~ s/$;5/\"/go;
    return $text;
}

sub unprotect_html($)
{
    my $what = shift;
    # APA: Use
    # Character entity references (eg. &lt;)
    # instead of
    # Numeric character references (eg. &#60)
    $what =~ s/\&amp;/\&/go;
    $what =~ s/\&quot;/\"/go;
    $what =~ s/\&lt;/\</go;
    $what =~ s/\&gt;/\>/go;
    return($what);
}

sub t2h_print_label($;$)
{
    my $fh = shift;
    my $href = shift || $T2H_HREF{This};
    $href =~ s/.*#(.*)$/$1/;
    print $fh qq{<a name="$href"></a>\n};
}

#print "" .substitute_text(0, "text \@samp{some\n", "\n", "samp with newline\n");

#print "" .substitute_text(0, "\@defun {@`i Interactive @^{\@dotless{i} Command} isearch-forward\n");

#exit;

# main processing is called here
SetDocumentLanguage('en') unless ($T2H_WORDS);
# APA: There's got to be a better way:
$things_map{'today'} = T2h_i18n::pretty_date($T2H_LANG);
$T2H_TODAY = T2h_i18n::pretty_date($T2H_LANG);  # like "20 September 1993";
my $T2H_USER = "unknown";
if ($T2H_TEST)
{
    # to generate files similar to reference ones to be able to check for
    # real changes we use these dummy values if -test is given
    $T2H_TODAY = 'a sunny day';
    $T2H_USER = 'a tester';
    $THISPROG = 'texi2html';
    setlocale( LC_ALL, "C" );
} 
else
{ 
    # the eval prevents this from breaking on system which do not have
    # a proper getpwuid implemented
    eval { ($T2H_USER = (getpwuid ($<))[6]) =~ s/,.*//;}; # Who am i
    # APA: Provide Windows NT workaround until getpwuid gets
    # implemented there.
    $T2H_USER = $ENV{'USERNAME'} unless defined $T2H_USER;
}

# Set the default body text, inserted between <body ... >
$T2H_BODYTEXT = 'lang="' . $T2H_LANG . '" bgcolor="#FFFFFF" text="#000000" link="#0000FF" vlink="#800080" alink="#FF0000"' unless (defined($T2H_BODYTEXT));

# this is used in footer
unless (defined($T2H_ADDRESS))
{
    $T2H_ADDRESS = "by <i>$T2H_USER</i> " if $T2H_USER;
    $T2H_ADDRESS .= "on <i>$T2H_TODAY</i>";
}

open_file($docu);
pass0();
pass1();
pass2();
pass3();
pass4();


##############################################################################

# These next few lines are legal in both Perl and nroff.

.00 ;                           # finish .ig

'di			\" finish diversion--previous line must be blank
.nr nl 0-1		\" fake up transition to first page again
.nr % 0			\" start at page 1
'; __END__ ############# From here on it's a standard manual page ############
    .so @mandir@/man1/texi2html.1
